//******************************************************************************
// YAYA　ゴーストテンプレート
// コミュニケート辞書
//******************************************************************************


//******************************************************************************
//こちらから話しかける
//******************************************************************************

//この関数は現状メニューから呼ばれます
StartCommunicate
{
	//以下の関数は、隣に立っているゴーストを探して、
	//TalkTo_??? 関数を呼び出すものです。誰も居なければランダムトークをします。
	//yaya_tmpl_utilにあります。
	AYATEMPLATE.ExecuteTalkToTalk
}


//その他の人に話しかける
TalkToOther{
	if mode{
		"%(noxNeutral)Hey, %(res_reference0). How are you?"
		startcomm = 1
	}else{
		"%(noxNarSleep)(Nox is asleep. That won't do.)"
	}
	
}

TalkToNobody{
	if mode
		"%(noxNeutral)It's pretty quiet..."
	else
		"%(noxNarSleep)(There's nobody around. Nox is also asleep, so that wouldn't work.)"
}


//******************************************************************************
//話しかけられたので返事する
//******************************************************************************

//------------------------------------------------------------------------------
//OnCommunicateイベント
//------------------------------------------------------------------------------
OnCommunicate
{
	if reference[0] == "user" || reference[0] == "User"
	{
		//---- ユーザーからの話しかけ
		ReplyToUser
	}
	else
	{
		//---- ゴーストからの話しかけ
		//以下の関数は、ReplyTo_??? 関数を呼び出すものです。
		//yaya_tmpl_utilにあります。
		AYATEMPLATE.ExecuteReplyToTalk(reference[0])
	}
}

//------------------------------------------------------------------------------
//ユーザーからの話しかけ
//------------------------------------------------------------------------------
ReplyToUser
{
	if "name" _in_ reference[1] && "remember" _in_ reference[1]
	{
		//---- ユーザー名入力開始
		"%(noxNeutral)What's your name?\![open,inputbox,OnNameTeach,-1]\e"
		teachusername = 1
	}
	else
	{
		//---- ユーザー名入力開始以外
		if teachusername == 1
		{
			//---- 直前が「名前覚えて」だったので名前を覚える
			username = reference[1]
			"%(noxNeutral)Your name is %(username).\e"
			teachusername = 0
		}
		else
		{
			//---- 名前覚え以外のさまざまな会話への返事
			TalkToUser
		}
	}
}

//------------------------------------------------------------------------------
//ユーザーへの返事（名前覚え以外）
//------------------------------------------------------------------------------
TalkToUser
{	
	if mode{
		_text = TOLOWER(reference[1])
		if "hello" _in_ _text || "hi" == _text || "howdy" _in_ _text
		{
			"%(noxBase)Oh, hello. Is that all?\e"
		}
		elseif "how are you" _in_ _text
		{	
			if annoyance > 50{
				"%(noxAnnoyed)I'm a little bothered."
			}else{
				"%(noxBase)I'm doing fine, thank you.\e"
			}
			
		}
		else
		{
			"%(noxNarBase)(He quietly and intently listens.)\e"
		}
	}else{
		"%(noxNarSleep)(Nox is asleep.)"
	}
}

//------------------------------------------------------------------------------
//Talk from a ghost
//------------------------------------------------------------------------------


//Receiving dialogue from another ghost
ReplyToOther{
	
	if mode{
		_talk = TOLOWER(reference[1])
		res_reference0=reference[0]	//respond to this ghost
		
		if startcomm < 1{
			//we set res_reference to 0 (null) so no one gets stuck in a communication loop
			if "hello" _in_ _talk || "hi" _in_ _talk || "hey there" _in_ _talk
			{
				"%(noxBase)Hello, %(res_reference0). How are you?\e"
				res_reference0=0
			}else
			{
				"%(noxBase)What was that, %(res_reference0)?\e"
				res_reference0=0
			}
		}	
		
		if "how are you" _in_ _talk || "how're you" _in_ _talk{
			
			"I'm doing well, %(res_reference0).\e"
			res_reference0=0
		}
	}else{
		"%(noxSleep)Zzz... hmmmn...?"
	
	}
		
	
	startcomm = 0
}

