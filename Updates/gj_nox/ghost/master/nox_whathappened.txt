//******************************************************************************
// Nox Ghost
// What Happened Today Generation, Envelopes, and Dialogue
//******************************************************************************

//******************************************************************************
// Generate What Happened
//******************************************************************************


//Generate numbers that indicate how a friend of Nox's is doing on a given day. 
OnWhatHappenedToday{

	//character arrays in the format of:
	//location -- mood -- special -- outstanding mood change -- pronouns (if applicable)

	//VANTA
	_vantalocation = RAND(3 + mugkingdom)			//home, village, beach, ALL HAIL MUG KINGDOM!!!
	_vantamood = vanta[1] + RAND(10) - 5 + vanta[3]
	_vantaspecial =  (RAND(20) == 8)
	//moods go below 1 or exceed max
	if _vantamood <= 0{
		vanta = (_vantalocation,1,_vantaspecial,0)
	}elseif _vantamood < 60{
		vanta = (_vantalocation,_vantamood,_vantaspecial,0)
	}else{
		vanta = (_vantalocation,60,_vantaspecial,0)
	}
	
	
	//ROOK
	_rooklocation = RAND(3)							//Home, village, beach, Hungry Table Tavern
	_rookmood = rook[1] + RAND(10) - 5 + rook[3]
	_rookspecial =  (RAND(20) == 8)
	//moods go below 1 or exceed max
	if _rookmood <= 0{
		rook = (_rooklocation,1,_rookspecial,0)
	}elseif _rookmood < 80{
		rook = (_rooklocation,_rookmood,_rookspecial,0)
	}else{
		rook = (_rooklocation,80,_rookspecial,0)
	}
	
	
	//KORI/ANNE
	_korilocation = RAND(3)							//Village, Hungry Table Tavern, traveling
	_korimood = korianne[1] + RAND(10) - 5 + korianne[3]
	_korispecial =  (RAND(20) == 8)
	_korigender = RAND(2)
	//moods go below 1 or exceed max
	if _koriannemood <= 0{
		korianne = (_korilocation,1,_korispecial,0,_korigender)
	}elseif _korimood < 100{
		korianne = (_korilocation,_korimood,_korispecial,0, _korigender)
	}else{
		korianne = (_korilocation,100,_korispecial,0, _korigender)
	}
	
	
	if _korigender == 0{
		koriHe = "she"
		koriHim = "her"
		koriHis = "hers"
	}else{
		koriHe = "he"
		koriHim = "him"
		koriHis = "his"
	
	}

}

//******************************************************************************
// Talk About What Happened
//******************************************************************************

WhatHappenedDialogue{

	//character arrays in the format of:
	//location -- mood -- special -- outstanding mood change -- pronouns (if applicable)
	//vanta, rook, korianne
	
	//VANTA DIALOGUES
	//******************************************************************************
	
	//location (0)
	//home, village, beach, ALL HAIL MUG KINGDOM!!!
	case vanta[0]{
	
		//home
		when 0{
			"%(noxNeutral)Today I saw Vanta reading in his personal library. While he was reading, %(vantaMood)"
		}
		//village
		when 1{
			"%(noxNeutral)Vanta and myself were going around Nighthaven today. I looked over at him and %(vantaMood)"
		}
		//beach
		when 2{
			""
		}
		//Mug Kingdom
		when 3{
			"%(noxCurious)When I got home today, I saw Vanta making a book castle next to that pile of mugs you gave me. Looking at him build, %(vantaMood) Really, you gave me an outrageous amount of mugs."
		}
		others{
			"[INDEX OUT OF BOUNDS ERROR: vanta[0]"
		
		}
	}
	
	
	
	//ROOK DIALOGUES
	//******************************************************************************
	
	//location (0)
	//Home, village, beach, Hungry Table Tavern
	case rook[0]{
	
		//home
		when 0{
			"%(noxNeutral)Rook dropped by for a visit by my house earlier. He was looking for Vanta, but he wasn't home. I asked him how he was doing and %(rookMood)"
		}
		//village
		when 1{
			"I saw Rook walk into Nighthaven, carrying some imports from the docks. Walking past, %(rookMood)"
		}
		//beach
		when 2{
			"Rook was out on the beach searching for.\w8.\w8.\w8 %(beachshiny) I think? Observing him, %(rookMood)"
		}
		//Hungry Table Tavern
		when 3{
			"Rook came with me to Glitterstone, and of course we went to The Hungry Table to visit some friends. I had to hide in the shadows for the whole trip, but as for Rook, %(rookMood)"
		}
		others{
			"[INDEX OUT OF BOUNDS ERROR: rook[0]"
		
		}
	}
	
	
	
	//KORIANNE DIALOGUES
	//******************************************************************************
	
	//location (0)
	//Village, Hungry Table Tavern, traveling
	case korianne[0]{
	
		//village
		when 0{
			"%(kori) dropped by Nighthaven for a visit. It's so rare to see %(koriHim) there, so of course I had to have a chat with %(koriHim). Talking with %(koriHim), %(korianneMood)"
		}
		//Hungry Table Tavern
		when 1{
			"I happened to see %(kori) in The Hungry Table while I was lurking in there. While I couldn't talk, %(korianneMood)"
		}
		//traveling
		when 2{
			"I came across %(kori) while %(koriHe) was out traveling. We discussed travel routes and %(korianneMood)"
		}
		others{
			"[INDEX OUT OF BOUNDS ERROR: korianne[0]"
		
		}
	}
	
	
	
	
	//SAME PLACE DIALOGUES (0)
	//******************************************************************************
	
	if vanta[0] == 0 && rook[0] == 0{
		"I came home to see both Vanta and Rook enjoying each other's company. Not surprising, considering they are boyfriends."
		
	}
	
	if vanta[0] == 1 && rook[0] == 1{
		"Rook took Vanta and myself around Nighthaven, and we checked out the markets together."
	
	}
	
	if vanta[0] == 2 && rook[0] == 2{
		"I noticed Rook and Vanta were out on the beach together. Looking over at them, I think they were looking for %(beachshiny) to fuel Rook's collecting addiction."
	}
	
	if rook[0] == 1 && korianne[0] == 0{
		"%(kori) and Rook ran into each other in Nighthaven. They... politely acknowledged each other and went their separate ways."
	
	}
	
	if rook[0] == 3 && korianne[0] == 1{
		"Rook was messing around at The Hungry Table, as he usually does. %(kori) happened to be there, but %(koriHe) was just shaking %(koriHis) head at Rook's shenanigans."
	}
	
	
	//SPECIAL EVENTS (2)
	
	if vanta[2] == 1{
		"%(noxNeutral)I was taking a quiet stroll in the forest next to Nighthaven, when I heard some odd sounds. Taking to the shadows to investigate it, I saw Vanta practicing his magic on his own.\n\nI quietly observed him and %(vantaMood)"
	}
	
	if korianne[2] == 1{
		
		if korianne[4] == 0{
			"Today Korianne gave me a scarf out of the blue. I think she accidentally made one too big for a human, which is understandable. When she gave me it, %(korianneMood)"
		}else{
			"On occasion, I'm able to help Kori out with his doctor work. While we were working, %(korianneMood)"
		}
	
	}
	



}







//******************************************************************************
// To Describe Mood
//******************************************************************************

//Vanta's max is 60
vantaMood{

	//Really good
	if vanta[1] > 50{
		"he seemed to be in high spirits, I've never seen him smile so much."
		"he seemed to be in high spirits."
		"he seemed to be more than content. It was a nice sight."
		"he was just about grinning."
	
	//Good
	}elseif vanta[1] > 36{
		"he had a gentle smile on his face."
		"he had a soft smile."
		"he seemed peaceful."
		"he was quite relaxed."
	
	//Neutral
	}elseif vanta[1] > 24{
		"he seemed like he was doing okay."
		"he seemed fine."
		"he seemed alright."
		"he was maybe tired, but overall okay."
	
	//Bad
	}elseif vanta[1] > 12{
		"%(noxAnnoyed)he looked like something was troubling him, I worry about him sometimes."
		"%(noxAnnoyed)he looked like something was troubling him."
		"%(noxAnnoyed)he seemed like something was wearing on him."
		"he seemed.\w4.\w4.%(noxAnnoyed)\w[22] quite worn."
	
	//Really Bad
	}else{
		"%(noxAnnoyed)he was crying a lot. I wonder if I did anything wrong..."
		"%(noxAnnoyed)he was crying a lot."
		"%(noxAnnoyed)he was sobbing."
		"%(noxAnnoyed)he was sobbing. I wonder what happened..."
		"%(noxAnnoyed)he looked like he was ready to break down, but I'm not sure why..."
	
	}

}


//Rook's max is 80
rookMood{
	//Really good
	if rook[1] > 66{
		"he seemed like he didn't have a single care in the world."
		"he seemed so perfectly carefree."
		"he seemed carefree-- nothing in the world could touch him."
		"he seemed like he didn't have a single care in the world. It was nice to see him like that."
	
	//Good
	}elseif rook[1] > 48{
		"he was full of his witty charm as usual."
		"he was full of his witty charm and cracking jokes."
		"he was making jokes and getting along well."
		"he was enjoying himself thoroughly and tossing around witty remarks."
	
	//Neutral
	}elseif rook[1] > 32{
		"he looked to be his chipper self."
		"he looked to be as chipper as normal for him."
		"he seemed chipper, as per usual."
		"he looked to be doing quite well."
	
	//Bad
	}elseif rook[1] > 16{
		"%(noxAnnoyed)he looked like he was really frustrated."
		"%(noxAnnoyed)he looked frustrated. I'm not sure why."
		"%(noxAnnoyed)he looked quite frustrated, and perhaps a bit bored."
		"%(noxAnnoyed)he looked like he was really frustrated."
	
	//Really Bad
	}else{
		"%(noxAnnoyed)he was unusually silent. I wonder if something happened to him."
		"%(noxAnnoyed)he was unusually silent."
		"%(noxAnnoyed)he was... oddly quiet. It was very strange."
		"%(noxAnnoyed)he wasn't talking much, and not even laughing along with others' jokes."
	
	}

}

//Kori/anne's max is 100
korianneMood{
	//Really good
	if korianne[1] > 82{
		"%(noxNeutral)%(koriHe) actually cracked a smile for once."
		"%(noxNeutral)%(koriHe) actually cracked a smile!"
		"%(noxNeutral)%(koriHe) was genuinely smiling."
		"%(noxNeutral)%(koriHe) was smiling like the world was at peace."
	
	//Good
	}elseif korianne[1] > 60{
		"%(noxNeutral)%(koriHe) looked like %(koriHe) was happy."
		"%(noxNeutral)%(koriHe) looked pretty happy."
		"%(noxNeutral)%(koriHe) seemed a little more relaxed than normal."
		"%(noxNeutral)%(koriHe) looked like %(koriHis) day was going well."
	
	//Neutral
	}elseif korianne[1] > 40{
		"%(noxNeutral)%(koriHe) seemed to be content."
		"%(noxNeutral)%(koriHe) seemed to be doing okay."
		"%(noxNeutral)%(koriHe) seemed like %(koriHe) was doing the same as usual."
		"%(noxNeutral)%(koriHe) seemed content enough."
	
	//Bad
	}elseif korianne[1] > 20{
		"%(noxCurious)I couldn't tell, but %(koriHe) looked upset."
		"%(noxCurious)It was hard to tell, but %(koriHe) looked somewhat upset."
		"%(noxCurious) %(koriHe) looked upset."
		"%(noxCurious)I couldn't quite tell, but %(koriHe) seemed... off."
	
	//Really Bad
	}else{
		"%(noxAnnoyed)%(koriHe) was harder to read than ever."
		"%(noxAnnoyed)%(koriHe) was harder to read than usual. Strange..."
		"%(noxAnnoyed)%(koriHe) had a steely, unreadable air to %(koriHim). "
		"%(noxAnnoyed)%(koriHe) had an expression as if %(koriHe)'d shut down..."
	
	}

}

//return the proper name based on gender
kori{

	if korianne[4] == 0{
		"Korianne"
	}else{
		"Kori"
	}

}

