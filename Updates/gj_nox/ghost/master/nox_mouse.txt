//******************************************************************************
// Yaya ghost template
// Mouse input related dictionary
//******************************************************************************


//******************************************************************************
// Mouse processing
//
// Describe the actual talk below.The function name has the following rules:
//[Type] + [scope] + [site]
//If you want to change the value of a property, you can set the value of the property to the value of the property you want to use.
//Scope is the scope number\0,\1,and so on.
// Site represents which part of the hit judgment.In case of an event where there is no hit determination
//There are no / sites.
//See bottom of document for friendship increment/decrement functions.
//******************************************************************************


//******************************************************************************
//撫で反応
//******************************************************************************
MouseMove0Head
{
	lastinteraction = 0
	if petsthishour > MAXPETS{
		relationship += RAND(2)+1
	}
	pets++
	petsthishour++
	if relationship > ANNOYEDR2{
		"%(noxCurious)Oh? I don't think I have ever been petted up there before."
		"%(noxNeutral)Huh. This oddly feels nice."
	
	}
	"%(MouseMoveGeneral)"
	--
	"\e"
}

MouseMove0Face
{
	lastinteraction = 0
	if petsthishour > MAXPETS{
		relationship += RAND(2)
	}
	pets++
	petsthishour++
	
	"%(MouseMoveGeneral)"
	
	if relationship > ANNOYEDR2{
		{
			"%(noxNeutral)That's my face- Why are you petting there?"
			"%(noxCurious)Are you trying to pet my nose? I would suggest under my chin more."
		}
		{
			"%(noxNeutral)%(noxEyesClosed)...You poked me in the eye.\w[16]%(noxNeutral) Oh wait, I don't have normal eyes."
			"%(noxNeutral)Why are you petting my face?"
		}
		
		if relationship > POSITIVER2{
			"%(noxCurious)Why do you insist on petting my face? I don't mind, but I would prefer other places."
			"%(noxCurious)Hmm. I'm not sure if I wholly like pets there."
			"%(noxNeutral)You know, it would be a lot easier to reach my chin- %(noxCurious)Wait.\w[16] No I suppose I'm still tiny here."
		
		}
		
	
	}elseif relationship < ANNOYEDR2{
		"%(noxNarAnnoyed)(He growls at you.)"
		"%(noxNarAnnoyed)(He bites your mouse cursor.)\n%(noxAnnoyed) That's enough. Stop it."
		"%(noxAnnoyed)Would you stop that?"
		"%(noxNarAngry)(He bites your mouse cursor. It doesn't hurt, but he still bit it.)"

	
	}
}

MouseMove0Chin
{
	lastinteraction = 0
	if petsthishour > MAXPETS{
		relationship += RAND(3)+1
	}
	pets++
	petsthishour++
	
	"%(MouseMoveGeneral)"
	
	if relationship > ANNOYEDR2{
		{
			"%(noxNarNeutral)%(noxEyesClosed)(Is he… purring?)"
			"%(noxNarNeutral)%(noxEyesClosed)(He seems to enjoy that spot.)"
		}
		{
			"%(noxNeutral)%(noxEyesClosed)Mmm yes. \w8That's the spot."
			"%(noxNarNeutral)%(noxEyesClosed)(He seems incredibly pleased.)"
		}
		
		if relationship > POSITIVER2{
			"%(noxNeutral)Vanta likes to pet me there all the time. I'm not sure why, but it feels really calming to me."
			"%(noxNeutral)%(noxEyesClosed)Mmmm that's the good spot."
		
		}
		
	
	}elseif relationship < ANNOYEDR2{
		"%(noxNarAnnoyed)(He grumbles a bit, but it seems like he's letting you pet there.)"
		"%(noxNarAnnoyed)(He lets you scritch under his chin.)"
		"%(noxNarAnnoyed)(He goes to bite you, but then seems to think otherwise of it.)"
		"%(noxAnnoyed)What do you hope to gain from petting me?"

	
	}else{
		//neutral pet
		"this is a bug if it occurs rip\e"
	
	}
	--
	"\e"

}

MouseMove0Wings{

	lastinteraction = 0
	relationship+= RAND(4) - 3
	annoyance-= RAND(4) - 3
	
	wingtouch++
	
	if relationship > ANNOYEDR1{
		"%(noxCurious)Ah- I would prefer if you didn't touch there. That feels weird."
		"%(noxAnnoyed)Please don't."
		"%(noxCurious)Please don't touch me there."
		"%(noxAnnoyed)That feels weird."
		"%(noxAnnoyed)I really don't like that."
	
	}else{
		"%(noxNarAngry)(He's growling at you.)"
		"%(noxAnnoyed)Do you want to lose a hand?"
		"%(noxAngry)I may be a shadow in physical form, but I can still bite."
		"%(noxAngry)Touch me there again, and I will bite you."
		"%(noxNarAngry)(Maybe that's not a good idea.)"
		"%(noxNarAngry)(He bites your mouse cursor. It doesn't hurt, but he still bit it.)"
	
	}
	
	
	{
		"%(noxAnnoyed)%(roar) %(growl)"
		"%(noxAnnoyed)%(growl) %(roar)"
		"%(noxNarAnnoyed)(You pet the dragon. He seems annoyed by this.)"
	
	}
	--
	"\e"


}

MouseMoveGeneral{

	if relationship > ANNOYREDR1{
		"%(noxNeutral)%(noxEyesClosed)%(roar) %(growl)"
		"%(noxNeutral)%(noxEyesClosed)%(growl) %(roar)"
		"%(noxNarNeutral)(You pet the dragon. He seems okay with this.)"
	
	}else{
		"%(noxAnnoyed)%(roar) %(growl)"
		"%(noxAnnoyed)%(growl) %(roar)"
		"%(noxNarAnnoyed)(You pet the dragon. He seems annoyed by this.)"
	}

}

//******************************************************************************
//つつかれ反応
//******************************************************************************

//本体側のつつかれ反応
MouseDoubleClick0Head
{	
	MouseDoubleClickAny
}

MouseDoubleClick0Face
{
	MouseDoubleClickAny
}


MouseDoubleClickAny : sequential{
	lastinteraction = 0
	punches++
	subFriendshipBig
	--
	if relationship > POSITIVER2{
		"%(noxCurious)Wh- Why?? What was that for?"
		"%(noxNarCurious)(He looks at you wide eyed.)"
		"%(noxAnnoyed)I thought we were friends."
		"%(noxAnnoyed)%(noxEyesClosed)I…\w8\w8 I thought we were friends…"
		"%(noxAnnoyed)These punches don't even hurt. What hurts more is the feeling of betrayal."
		relationship-= 30
	
	}elseif relationship > ANNOYEDR2{
		"%(noxCurious)Excuse me, but what was that for?"
		"%(noxCurious)Ow?"
		"%(noxCurious)What are you trying to accomplish here?"
		"%(noxAnnoyed)That's annoying. Please stop."
		"%(noxAnnoyed)Please stop."
		"%(noxCurious)What was that for?"
	
	}else{
		//neutral hit
		"%(noxNarAngry)(He recoils in anger.)"
		"%(noxNarAngry)(He bites your mouse cursor in return.)"
		"%(noxNarAngry)(He bites your mouse cursor. It doesn't hurt, but he still bit it.)"
		"%(noxAngry)%(growl) %(growl) %(roar)"
		"%(noxAngry)Would you knock that off?"
	
	}
	--
	"\e"

}


MouseDoubleClick0
{
	//顔以外のつつきでメニュー展開
	OpenMenu
}

//******************************************************************************
//マウス処理部コア
//AYATEMPLATE.MouseMoveWheelFuncの2番目の数字が「感度」です。
//1番目の数字（0か1）は、MoveかWheelかを判別するためのもので、
//変更の必要はありません。
//******************************************************************************

OnMouseMove
{
	AYATEMPLATE.MouseMoveWheelFunc(0,50,'MouseMove')
}

OnMouseWheel
{
	_result = ''
	if TOINT(reference[2]) > 0 {
		_result = AYATEMPLATE.MouseMoveWheelFunc(1,4,'MouseWheelUp')
	}
	else {
		_result = AYATEMPLATE.MouseMoveWheelFunc(1,4,'MouseWheelDown')
	}
	
	if _result == '' {
		_result = AYATEMPLATE.MouseMoveWheelFunc(1,4,'MouseWheel')
	}
	
	_result
}

//Normally I'd leave this alone, but for the sake of Tendie Game I must interfere
OnMouseDoubleClick
{
	//reference3 is the character number
	if reference3 > 0{
		OnClickTendril
	
	}else{
		AYATEMPLATE.MouseEventExec('MouseDoubleClick');
	}
	
}



addFriendship{
	relationship++

}

addFriendshipBig{
	relationship+=3

}


subFriendship{
	if relationship > POSITIVER2{
		relationship-=2
		annoyance+=2
	}elseif relationship < ANNOYEDR2{
		relationship-=2
		annoyance+=2
	}else{
		relationship--
		annoyance++
	}

}


subFriendshipBig{
	if relationship > POSITIVER3{
		relationship-=13
		annoyance+=13
	}elseif relationship > POSITIVER2{
		relationship-=8
		annoyance+=8
	}elseif relationship > POSITIVER1{
		relationship-=5
		annoyance+=5
	}elseif relationship > ANNOYEDR1{
		relationship-=3
		annoyance+=3
	}elseif relationship > ANNOYEDR2{
		relationship-=4
		annoyance+=4
	}elseif relationship > ANNOYEDR3{
		relationship-=6
		annoyance+=6
	}else{
		relationship-=8
		annoyance+=8
	}

}





























