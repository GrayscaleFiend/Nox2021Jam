//******************************************************************************
// YAYA　Ghost Template
// Miscellanious Event Dictionaries
//******************************************************************************

//******************************************************************************
//For switching shells
//******************************************************************************

//------------------------------------------------------------------------------
//Before
//------------------------------------------------------------------------------

OnShellChanging
{
	"%(noxCurious)You want a different color? Alright.\s[-1]\w8\w8\w8\e"
}

//------------------------------------------------------------------------------
//After
//------------------------------------------------------------------------------
OnShellChanged
{	
	//reference[0] is the new shell's name
	
	"%(noxBase)\e"
}


//******************************************************************************
//Installation related
//******************************************************************************

//------------------------------------------------------------------------------
//Message during installation
//------------------------------------------------------------------------------
OnInstallBegin
{
	"%(noxNeutral)Installing %(reference[0]).\e"
}

//------------------------------------------------------------------------------
//インストール完了メッセージ
//------------------------------------------------------------------------------
OnInstallComplete
{
	_ref0 = SPLIT(reference[0],' with',2)[0] //hogehoge with hemohemo
	
	"%(noxNeutral)Oh, it's a "
	--
	if _ref0 == "shell" {
		"shell"
	}
	elseif _ref0 == "ghost" {
		"ghost"
	}
	elseif _ref0 == "balloon" {
		"balloon"
	}
	elseif _ref0 == "plugin" {
		"plugin"
	}
	--
	".\n\n\w8\w8"
	--
	"%(reference[1]) installation complete.\n"
	--
	if basewarenameex == 'SSP' {
		_id = ''
		if _ref0 == "shell" {
			_id = 'shell'
		}
		elseif _ref0 == "ghost" {
			_id = 'ghost'
		}
		elseif _ref0 == "balloon" {
			_id = 'balloon'
		}
		
		if _id != '' {
			"What do you want me to do with it?"
			--
			AYATEMPLATE.MenuItem("Switch","OnAYATEMPLATE.InstallChange",_id,200)
			--
			AYATEMPLATE.MenuItem("Nothing","Menu_CANCEL")
		}
	}
}

OnAYATEMPLATE.InstallChange
{
	"\![change,%(reference[0]),lastinstalled]"
}

//------------------------------------------------------------------------------
//インストール失敗メッセージ
//------------------------------------------------------------------------------
OnInstallFailure
{
	"That didn't work.\e"
}

//------------------------------------------------------------------------------
//インストール拒否（他のゴーストのためのアーカイブだった）
//------------------------------------------------------------------------------
OnInstallRefuse
{
	"%(noxNeutral)I can't do that.\e"
}


//******************************************************************************
//消滅関連
//******************************************************************************

//------------------------------------------------------------------------------
//When you select the uninstall option
//------------------------------------------------------------------------------
OnVanishSelecting
{
	"%(noxCurious)Are you sure.\w4.\w4.?\e"
}

//------------------------------------------------------------------------------
//When you confirm the uninstall
//------------------------------------------------------------------------------
OnVanishSelected
{
	if RAND(100) == 0{
		"%(noxNeutral)Change da world, \w8my final message.\w8\w8 Goodbye\w4"
	}else{
		"%(noxAnnoyed)Alright. Goodbye.\\w8e"
	}
	
}

//------------------------------------------------------------------------------
//When you change your mind and deny the uninstall
//------------------------------------------------------------------------------
OnVanishCancel
{
	"%(noxAnnoyed)That's not funny.\e"
}

//------------------------------------------------------------------------------
//When this ghost is switched to after you uninstall a different ghost
//------------------------------------------------------------------------------
OnVanished
{
	relationship-=5
	"%(noxCurious)What happened to %reference[0]...?\e"
}


//******************************************************************************
//メールチェック
//******************************************************************************

//------------------------------------------------------------------------------
//Start email check
//------------------------------------------------------------------------------
OnBIFFBegin
{
	"%(noxNeutral)Checking email\e"
}

//------------------------------------------------------------------------------
//Success
//------------------------------------------------------------------------------
OnBIFFComplete
{
	if reference[0] == 0
	{
		//---- メールは無かった
		"%(noxNeutral)nothing new\e"
	}
	else
	{
		//---- メールが届いていた
		"%(noxNeutral)%(reference[0])new email(s)\e"
	}
}

//------------------------------------------------------------------------------
//メールチェック失敗
//------------------------------------------------------------------------------
OnBIFFFailure
{
	if reference[0] == "timeout"
	{
		//---- タイムアウト
		"%(noxNeutral)Timed out.\e"
	}
	elseif reference[0] == "kick"
	{
		//---- 蹴られた
		"%(noxNeutral)Server kicked.\e"
	}
	elseif reference[0] == "defect"
	{
		//---- 設定誤り
		"%(noxNeutral)It's defected.\e"
	}
	else
	{
		//---- 原因不明
		"%(noxNeutral)Yeah ok it's just not working.\e"
	}
}



//******************************************************************************
//Network Updates
//******************************************************************************

//------------------------------------------------------------------------------
//Start update
//------------------------------------------------------------------------------
OnUpdateBegin
{
	"%(noxNarBase)Looking at the shadows...\e"
	"%(noxNarBase)Merging with the shadows...\e"
	"%(noxNarBase)Merging into the shadows...\e"
}

//------------------------------------------------------------------------------
//An update is found successfully
//------------------------------------------------------------------------------
OnUpdateReady
{
	"%(noxNeutral)\_q%(TOINT(reference[0])+1) elements located.\e"
}

//------------------------------------------------------------------------------
//Network update finished
//------------------------------------------------------------------------------
OnUpdateComplete
{
	if reference[0] == "none"
	{
		//---- no new files
		//positive
		if relationship > ANNOYEDR1{
			"%(noxNeutral)Hm, nothing has changed in the order.\e"
		
		//negative route
		}else{
			"%(noxAnnoyed)Things are the same as always.\e"
		}
		
	}
	else
	{
		//---- genuine completion
		"%(noxBase)Everything is in.\w4.\w4. proper order.\e"
	}
}

//------------------------------------------------------------------------------
//Network update failure
//------------------------------------------------------------------------------
OnUpdateFailure
{
	if reference[0] == "timeout"
	{
		//---- タイムアウトした
		"%(noxAnnoyed)Update timed out."
	}
	elseif reference[0] == "md5 miss"
	{
		//---- MD5値が一致しなかった
		"%(noxAnnoyed)The MD5 value isn't aligned."
	}
	else
	{
		//---- 上記以外の理由で失敗した
		"%(noxAnnoyed)%(reference[0]) error--"
	}
	--
	"\n\nPerhaps you should bring this up to someone.\e"
}

//------------------------------------------------------------------------------
//Update begins for a specific file
//------------------------------------------------------------------------------
OnUpdate.OnDownloadBegin
{
	"%(noxNeutral)%(reference[0]) done.\e"
}

//------------------------------------------------------------------------------
//Check MD5
//------------------------------------------------------------------------------
OnUpdate.OnMD5CompareBegin
{
	"\_q"
	--
	"%(noxNarBase)(Checking the stars...)\e"
	"%(noxNarBase)(Looking for accurate reflections...)\e"
	"%(noxNarBase)(Aligning shadows...)\e"
}

//------------------------------------------------------------------------------
//ネットワーク更新 MD5値一致
//------------------------------------------------------------------------------
OnUpdate.OnMD5CompareComplete
{
	"%(noxNarBase)\_q(They're aligned.)\e"
}

//------------------------------------------------------------------------------
//Bad md5
//------------------------------------------------------------------------------
OnUpdate.OnMD5CompareFailure
{
	"%(noxBase)That's not right.\e"
	"%(noxBase)They're not matched up.\e"
}

//******************************************************************************
//ヘッドラインセンス
//******************************************************************************

//------------------------------------------------------------------------------
//ヘッドラインセンス開始
//------------------------------------------------------------------------------
OnHeadlinesenseBegin
{
	//reference[0] is what the headlines are from (ie ref0 = Game Watch)
	"%(noxNeutral)Ah, here we are.\e"
}

//------------------------------------------------------------------------------
//ヘッドライン読み上げ
//------------------------------------------------------------------------------
OnHeadlinesense.OnFind
{
	//---- はじめのセリフ
	if reference[2] == "First" || reference[2] == "First and Last"
	{
		//---- 最初のページ
		"\0\b2%(noxNeutral)%(reference[0])"
	}
	elseif reference[2] == "Next"
	{
		//---- 真ん中あたりのページ
		"\0\b2%(noxNeutral)%(reference[0]), cont."
	}
	elseif reference[2] == "Last"
	{
		//---- 最後のページ
		"\0\b2%(noxNeutral)%(reference[0]), end."
	}
	--
	//---- ヘッドライン本体
	"\n\n%(reference[3])\n\n"
	--
	//---- 選択肢
	if reference[2] == "First" || reference[2] == "Next"
	{
		//---- 次のページがある場合
		AYATEMPLATE.MenuItem("Next Page","")
		--
		AYATEMPLATE.MenuItem("To Site",reference[1])
		--
		AYATEMPLATE.MenuItem("End","Menu_HEADLINECANCEL",150)
	}
	else
	{
		//---- 次のページがない場合
		AYATEMPLATE.MenuItem("To Site",reference[1])
		--
		AYATEMPLATE.MenuItem("End","Menu_HEADLINECANCEL",150)
	}
}

//------------------------------------------------------------------------------
//No headline update
//------------------------------------------------------------------------------
OnHeadlinesenseComplete
{
	"%(noxNeutral)%(reference[0]) has nothing new.\e"
}

//------------------------------------------------------------------------------
//ヘッドラインセンス失敗
//------------------------------------------------------------------------------
OnHeadlinesenseFailure
{
	"%(noxNeutral)It failed, because "
	--
	if reference[0] == "can't download"
	{
		"I can't download it."
	}
	elseif reference[0] == "can't analyze"
	{
		"I can't understand it."
	}
	else
	{
		"...well, I'm not sure..."
	}
	--
	"\e"
}


//******************************************************************************
//File creation
//******************************************************************************

//------------------------------------------------------------------------------
//updates2.dau creation start
//------------------------------------------------------------------------------
OnUpdatedataCreating
{
	"%(noxNarBase)(Assembling data...)\e"
	"%(noxNarBase)(Organizing your components...)\e"
}

//------------------------------------------------------------------------------
//updates2.dau作成完了
//------------------------------------------------------------------------------
OnUpdatedataCreated
{
	"%(noxBase)Update data assembled and shadows put where needed.\e"
}

//------------------------------------------------------------------------------
//NAR作成開始
//------------------------------------------------------------------------------
OnNarCreating
{
	if relationship > ANNOYEDR2{
		"%(noxNeutral)I'll prepare those elements for you.\w4.\w4.\e"
	}else{
		"%(noxAnnoyed)Fine, I'll prepare it for you.\e"
	}
	
}

//------------------------------------------------------------------------------
//NAR作成完了
//------------------------------------------------------------------------------
OnNarCreated
{
	"%(noxNeutral)NAR assembled.\e"
}


//******************************************************************************
//Clock adjustment
//Reffered to Mika's dictionary: (http://homepage2.nifty.com/ko-ki/ ) Thanks, Mr. Koki
//(the above seems broke, but I'll leave it there in case)
//******************************************************************************

//------------------------------------------------------------------------------
//Start clock setting
//------------------------------------------------------------------------------
OnSNTPBegin
{
	"%(noxNeutral)Connecting %(reference[0])\e"
}

//------------------------------------------------------------------------------
//時刻取得成功、比較開始
//この関数で 2008/03/25 19:15:05 のように見やすい形に作り変えます。
//詳しくはSTRFORM関数の説明を見てね！
//わからなければこのままで。
//------------------------------------------------------------------------------

SNTPCompare_StrForm
{
	_txt = SPLIT(_argv[0],',')
	STRFORM("$04d/$02d/$02d $02d:$02d:$02d",TOINT(_txt[0]),TOINT(_txt[1]),TOINT(_txt[2]),TOINT(_txt[3]),TOINT(_txt[4]),TOINT(_txt[5]))
}

OnSNTPCompare
{
	"%(noxNeutral)\_q/
	Before %(SNTPCompare_StrForm(reference[2]))\n/
	After  %(SNTPCompare_StrForm(reference[1]))\n/
	\_q\n"
	--
	if reference[3] == 0
	{
		"Everything seems to be in order, %username.\e"
	}
	else
	{
		//\6は補正実行タグ。消しちゃだめ
		"\6\0\n%(noxNeutral)%(reference[3]) seconds corrected."
	}
}

//------------------------------------------------------------------------------
//時計合わせ失敗
//------------------------------------------------------------------------------
OnSNTPFailure
{
	"%(noxNeutral)The clock adjustment... failed. That is certainly odd.\e"
}


//******************************************************************************
//そのほか
//******************************************************************************

//------------------------------------------------------------------------------
//ディスプレイ解像度変更
//------------------------------------------------------------------------------
OnDisplayChange
{
	//---- 変数にディスプレイ解像度と色深度に関する情報を取得
	//---- 使うかどうかは分からないがとりあえず保持しておく
	displaybpp    = reference[0]
	displaywidth  = reference[1]
	displayheight = reference[2]
}

//i dont't think this is working right: note to self for when I can have Multiple Tendies
OnDisplayHandover{
	displaynumber = reference[3]

}

//------------------------------------------------------------------------------
//URLドロップ
//CROW でゴーストインストーラからゴーストをインストールを始めた時にも呼ばれます
//------------------------------------------------------------------------------
OnURLDropping
{
	"%(noxNeutral)Oh, let's see... %(reference[0])\e"
}

//------------------------------------------------------------------------------
//起動時に MATERIA を検出
//------------------------------------------------------------------------------
OnEmbryoExist
{
	"%(noxNeutral)embryo\e"
}

//------------------------------------------------------------------------------
//起動時に猫どりふを検出
//------------------------------------------------------------------------------
OnNekodorifExist
{
	"%(noxNeutral)nekodori\e"
}

//------------------------------------------------------------------------------
//スクリーンセーバー実行開始
//------------------------------------------------------------------------------
OnScreenSaverStart
{
	"%(noxNeutral)Oh, it's dark!\w8\e"
}

//------------------------------------------------------------------------------
//スクリーンセーバー実行終了
//------------------------------------------------------------------------------
OnScreenSaverEnd
{
	"%(noxNeutral)Back into the light.\e"
}



OnNoxLoremIpsum
{
	"Lorem ipsum dolor sit amet, consectetur adipiscing elit, sed do eiusmod tempor incididunt ut labore et dolore magna aliqua. Mi tempus imperdiet nulla malesuada pellentesque. Convallis posuere morbi leo urna molestie at elementum eu facilisis. Felis eget velit aliquet sagittis id consectetur. Urna porttitor rhoncus dolor purus non enim praesent elementum. Nibh sit amet commodo nulla facilisi nullam vehicula. Mi eget mauris pharetra et ultrices neque ornare aenean. Faucibus a pellentesque sit amet porttitor eget dolor morbi non. Eget felis eget nunc lobortis mattis aliquam faucibus purus. Turpis egestas sed tempus urna et pharetra pharetra. Amet tellus cras adipiscing enim eu. Congue nisi vitae suscipit tellus. Mauris pharetra et ultrices neque. Ac tortor vitae purus faucibus ornare suspendisse sed nisi lacus. Turpis egestas maecenas pharetra convallis posuere morbi leo urna molestie. Iaculis eu non diam phasellus vestibulum lorem sed. Nibh venenatis cras sed felis eget. Ac auctor augue mauris augue neque. Mi quis hendrerit dolor magna eget est lorem ipsum dolor. Ullamcorper sit amet risus nullam eget."
	--
	"\n\nTellus integer feugiat scelerisque varius morbi enim nunc. Malesuada fames ac turpis egestas sed tempus urna. Felis imperdiet proin fermentum leo vel. Nec tincidunt praesent semper feugiat nibh. Eget velit aliquet sagittis id. Quis blandit turpis cursus in hac habitasse. Sed egestas egestas fringilla phasellus faucibus scelerisque eleifend donec pretium. Porttitor eget dolor morbi non. In egestas erat imperdiet sed euismod nisi porta lorem mollis. Tellus pellentesque eu tincidunt tortor aliquam nulla facilisi cras fermentum.\e"
}