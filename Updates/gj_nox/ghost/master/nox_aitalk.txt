//******************************************************************************
// YAYA　ゴーストテンプレート
// トーク辞書
//******************************************************************************

//Constants -- defined in many places but for whatever reason it's not fully working and I need to consult the manual
#globaldefine ANNOYEDR3 -250
#globaldefine ANNOYEDR2 -150
#globaldefine ANNOYEDR1 -50
#globaldefine POSITIVER1 50
#globaldefine POSITIVER2 150
#globaldefine POSITIVER3 250

#globaldefine QPERHOUR 5
#globaldefine MAXPETS 5
#globaldefine MAXWEIGHT 20

//******************************************************************************
// ランダムトーク
//******************************************************************************
RandomTalk
{
    TOSTR(EVAL(CHR(0x22)+RandomTalkEx()+CHR(0x22)))
}

//------------------------------------------------------------------------------
//ランダムトーク本体：この中にランダムトークを記載してください
//ここのみ常に ' ' で括って書いてください。
//上のRandomTalkの中身で%( )を展開していますので、" "を使わなくてもOKです。
//高速化対策
//------------------------------------------------------------------------------
RandomTalkEx : nonoverlap
{
	_timeslot = GetTimeSlot
	"\b1"
	--
	
	//Modes: 0 asleep, 1 awake (normal)
	case mode{
	
	
	when 1{
		//ANY ROUTE
		{
			"%(noxCurious)What is this thing you are using? The in..ter...net?\w[16] Inter net? That's odd, it doesn't look like a net.\e"
			//NOTE TO SELF: in a later update actually check if Discord is open
			"This thing you are using is called Discord... You put words in it quickly. %(noxCurious)Is it a quick way to send letters? No that doesn't seem correct... hmm...\e:chain=whatsdiscord"
			"%(noxBase)You know, this place is different from any other place I've been to. There doesn't seem to be any natural shadows.\e"
			"%(noxBase)This place is strange. There is a completely lit up area, but it is only in a rectangular shape. Beyond that? Complete darkness as far as I can see.\e"
		}
		{
			"%(noxBase)I'm not sure why, but it feels like there's something off about this place."
			"%(noxNarNeutral)(He quietly starts humming a%(songfeel) tune to himself.)"
			"%(noxBase)There seems to be no way to tell time in this place. There's some tiny numbers, but I'm having a hard time reading them."
			"%(noxCurious)Have you ever wondered why the second moon only comes out when the first one is full?\n%(noxNarCurious)\w[16]........\n\w[16]%(noxAnnoyed)I certainly haven't."
			--
			"\e"
		}
		{
			"%(noxBase)%(roar) %(growl)."
			"%(noxBase)%(growl) %(roar)."
			"%(noxNarBase)(Dragon sounds commence.)"
			--
			"\e"
		
		
		}
		
		
	    //A1, N, P1, P2, P3
        if relationship > ANNOYEDR2{
			if _timeslot == "early" || _timeslot == "night"{
				"%(noxNeutral)%(noxEyesClosed)%(noxYawn)(He yawns) I'm starting to feel tired, but it's still light in here. How odd."
			}
			
			"%(noxBase)Are you able to see the stars where you are at, %(username)? They're beautiful. I love the night sky. Out of everything, it seems to be the only thing that never changes."
			"%(noxBase)What do you think of the moon? Personally, I'm not much of a fan."
			"%(noxBase)The other day I was walking through the %(geo) and saw some wild %(animals) roaming about. Have you ever seen one before?"
			"%(noxCurious)%(username), have you stood up and stretched recently? If not, I would suggest doing so."
			--
			"\e"
			
            //A1, A2, N, P1, P2
            if relationship < POSITIVER3{
                
            }
        
        }
        
        //N, P1, P2, P3
        if relationship > ANNOYEDR1{
			"%(noxNeutral)What kinds of food do you like, young one? I like more savory meals personally."
			"%(noxNeutral)What kinds of food do you like, %(username)? I like more savory meals personally."
			"%(noxCurious)Hey, are you taking care of yourself out there? If you haven't already, I would suggest getting a drink of water. Perhaps a snack too."
			--
			"\e"
		
        }else{
		//A1, A2, A3
			"%(noxAnnoyed)Why do you keep me trapped here? I have much better things to do than be stuck here with someone aggravating."
			"%(noxNarAnnoyed)(He's ignoring you.)"
			"%(noxNarAnnoyed)(He isn't paying attention to you.)"
			"%(noxNarAnnoyed)(He shot you a dirty look.)"
			
            //A1, A2
            if relationship < ANNOYEDR3{
            
            }
			
			--
			"\e"
        }
        
		
        //P1, P2, P3
        if relationship > POSITIVER1{
        
        
        }
		
        
        //P2, P3
        if relationship > POSITIVER2{
			"%(noxCurious)Have you ever met a mimic before? \w8\w8%(noxNeutral)I have. Some friends of mine own one named Table Scraps. He's friendly. \w8\w8For a table."
			"%(noxCurious)Sometimes I wonder why my house has a bunch of %(beachshiny). %(noxNeutral)Then I remember Rook visits often."
			--
			"\e"
        }
        
		
        //N, A1, A2, A3
        if relationship > ANNOYEDR1{
        
            //N, A1, A2
            if relationship > ANNOYEDR3{
            
            
            }
        }
        
		
        //A2, A3
        if relationship < ANNOYEDR2{
			"%(noxNeutral)Here's an idea. Why not let me go home?\n.\w8.\w8.\w8.\nNo?\w8 %(noxAnnoyed)Fine."
			--
			"\e"
        
        }
		
		
		
		
		if whathappened == 1{
		
			"%(WhatHappenedDialogue)"
			--
			"\e"
		}
	}
	
	when 0{
		"%(noxSleep)Zzzzzzzz..."
	
	
	
	}
		


	}
               
}

//------------------------------------------------------------------------------
//Chains
//------------------------------------------------------------------------------
whatsdiscord
{{CHAIN

	"%(noxNeutral)Aha! I got it! It's a way you can talk without using your mouth. I know someone who would love that.\e:chain=end"
	
}}CHAIN


//******************************************************************************
//Key pressed
//******************************************************************************
/*
//------------------------------------------------------------------------------
//OnKeyPress
//------------------------------------------------------------------------------
// This event fires when any key on the keyboard is pressed.
// When specifying the function key, specify "f1" etc.
// The pressed key is stored in reference0.
//It is better to branch with // if statement or case ~ when statement.
*/

OnKeyPress
{
	case reference0 {
		when "t" {
			'\![raise,OnAiTalk]'
		}
		when "r" {
			'\![reload,shiori](RELOAD SHIORI)'
		}
		when "m" {
			if ISFUNC('OpenMenu') {
				OpenMenu
			}
			else {
				'\![raise,OnMouseDoubleClick,0,0,0,0,__SYSTEM_KEYDOWN_COL,0,mouse]'
			}
		}
		when "p"{
		//	relationship++
		//	"%relationship\e"
		}
	//	when "-"{
	//		relationship--
	//		"%relationship\e"
	//	}
	}
}

//******************************************************************************
//時報/重なり
//******************************************************************************

//------------------------------------------------------------------------------
//OnMinuteChangeイベント
//------------------------------------------------------------------------------

OnMinuteChange
{
	"\b1"
	--
	
	//general updates
	lastinteraction++
	
	
	//------------------SLEEP MANAGEMENT
	if wakefulmovements >= 0
		wakefulmovements-=5		
	
	//if he is asleep, increase rest, see if he'll wake up
	if mode==0{
		timeslept++
		
		if (timeslept + wakefulmovements + RAND(60) ) > 540{
			OnNoxWakeUp
		
		}
		
		
	}else{
	//if awake, decrease it every other minute, see if he'll fall asleep
		if minute % 2 == 0
			timeslept--
		
		
		
		//he's exhausted, very likely to pass out every 5 minutes
		if timeslept <= -5{
			timeslept = 0
			
			//real likely to Conk, even if bad relationship
			if (!RAND(8)){
				OnNoxFallAsleep
			}
		
		}
		//he's gotten somewhat tired
		elseif timeslept < 180{
			
			//hours for his preferred sleep schedule
			if (hour < 10){
				
				//the more wakefulmovements, the less likely he'll conk out
				if (timeslept + wakefulmovements - RAND(100)) < 0 {
					OnNoxFallAsleep
				
				}
			
			}else{
				//otherwise, less likely
				if (timeslept + wakefulmovements + RAND(10) - (relationship/10)) < 0 {
					OnNoxFallAsleep
				
				}
			
			}
		
		}
	
	}
	
	
	//Minute checks for relationship tiers (for betrayal and recovery)
	
	if relationship > POSITIVER3{
		cdmultiplier = 3
		waspositive = 1
		
	}elseif relationship > POSITIVER2{
		cdmultiplier = 2
	
	}elseif relationship < ANNOYEDR3{
		cdmultiplier = 1
		wasnegative = 1
	
	}elseif relationship < ANNOYEDR2{
		cdmultiplier = 1
	
	}else{
		cdmultiplier = 1
	}
	
	
	//See if we should spawn a tendril -- after the user hasn't interacted with Nox in more than 3 minutes, 1 in 10 chance
	if activetendrils == 0 && tendiesallowed == 1 && lastinteraction > 3 && RAND(10) == 1{
		//Later after bugfixes, this will be OnInstantiateTendril, but for now... it's all the same.
		OnStartNoxTendrils
	
	}
	
	
	
	
	
	--
	//---- When it's a new hour
	if minute == 0
	{
		OnHourChange
		
	}
	elseif AYATEMPLATE.mikireflag == 1
	{
		//---- 分 != 0 、かつ見切れていたら見切れ中のトークを行う
		MikireTalk
	}
	elseif reference[2] == 1
	{
		//---- 分 != 0 、かつ見切れておらず本体とkeroが重なっていたら重なり処理
		//---- 大抵はkero側を引き離す処理を行う
		
		//yeah we have tendrils now, we don't need this.
		//"%(noxNeutral)Overlapping hopefully nonexistent kero.\4\e"
	}
}

OnHourChange{

	//variables
	questioncooldown = 0
	petsthishour = 0
	//with each hour here you hurt Vanta's feelings :(
	vanta[3]-=.5
		
	//can be turned off in settings
	if hourannounce >= 1 {
		if mode{
			"%(noxNarBase)"
		}else{
			"%(noxNarSleep)"
		}
		--
		//---- 特定の時間（正午など）ではさらに追加で話す
		case hour{
			when 0
			{
				"(It's midnight.)"
			}
			when 12
			{
				"(It's about lunchtime.)"
			}
			others
			{
				"(It's %(hour12).)"
			}
		}
		--
		"\e"
			
	}



}
//******************************************************************************
//見切れ
//******************************************************************************

//------------------------------------------------------------------------------
//OnSecondChangeイベント
//1 秒毎に実行される。ここではあまり重い処理を行わないこと
//このテンプレートでは、見切れ処理のみ行っている
//---------------------------------------------------------------------------
OnSecondChange
{

	if AYATEMPLATE.mikireflag == 1 && reference[1] == 0
	{
		//---- 見切れの終了
		EndMikireTalk
	}
	AYATEMPLATE.mikireflag = reference[1]
	
	if annoyance >= 100{
		OnEndTendrils //just in case ykno
		--
		OnForceClose
		
	
	}else{
	
		//IJHEBAFGTVLKJHBNTFDLJKHGBKDJFH IT'S MOSTLY WORKIN DUDE I'M GROWIN A PLACEHOLDER STICK
		//JERFDKVNGLFSDBLSKHJFDB DUDE IT'S NOT A STICK ANYMORE YEEHAW
		if tendrilgrow {
			"\![embed,OnGrowTendrils]"
		}
	}
	
	
}

OnNoxFallAsleep{
	mode=0
	"%(noxBase)Zzzz..."
}


OnWakeNox{
	wakeNox
	"%(noxBase)Hmmm...?"

}


OnNoxWakeUp{
	wakeNox
	"%(noxBase)...What do you want...?"
}


wakeNox{
	mode=1

}






//------------------------------------------------------------------------------
//見切れ中
//------------------------------------------------------------------------------
MikireTalk : nonoverlap
{
	//Nox only notices being offscreen if he's awake
	if mode{
		if offscreen == 0{
			offscreen = 1
			relationship -= RAND(5) + 1
		}
		if relationship > ANNOYEDR2{
			{
				"%(noxCurious)Would you mind moving me more onto the lighted area? It is pitch black beyond that border."
				"%(noxNeutral)I would much rather be in full view, if you wouldn't mind."
				"%(noxNeutral)Don't you think it would be better to see all of me?"
			}
			{
				"%(noxNeutral)I would like to be in the lighted area, if you would please."
				"%(noxCurious)If you wouldn't mind, could you move me into the lighted area?"
				"%(noxNarCurious)%(noxEyesClosed)(He looks nervous for some reason.)"
			}
			
			if relationship > POSITIVER1{
				"%(noxCurious)Hey… I don't like being in this darker area."
				"%(noxCurious)I don't want to be in the dark. Could you move me?"
			}
		}
		
		if relationship < ANNOYEDR1{
			"%(noxAnnoyed)If you are going to keep me here, could you at least move me into the lighted area?"
			"%(noxAnnoyed)I want to keep an eye on you. Can you move me to do just that?"
			"%(noxNarAnnoyed)%(noxEyesClosed)(He looks like he's nervous.)"
		}
		--
		"\e"
	}
}

//------------------------------------------------------------------------------
//見切れ終了
//------------------------------------------------------------------------------
EndMikireTalk
{
	//anything non zero is awake
	if mode{
		offscreen = 0
		relationship+= RAND(4) + 1
		"%(noxBase)Thank you.\e"
		"%(noxNarNeutral)%(noxEyesClose)(He seems to be a little more at ease.)\e"
		"%(noxNeutral)That’s better."
	}
}


//------------------------------------------------------------------------------
//見切れ後の初回のランダムトークは見切れ関連のトークを行う。
//これをチェインで実現している
//------------------------------------------------------------------------------

//******************************************************************************
//サーフィス復元
//******************************************************************************

//******************************************************************************
//トランスレータ
//******************************************************************************

//------------------------------------------------------------------------------
//OnTranslateイベント
//------------------------------------------------------------------------------
//もっとも単純な例。語尾変え（「。」→「にゅ。」）。
//動かないようにコメントアウトしています。

//the OnTranslate function so that script input debug can actually happen
OnTranslate
{
	
    _talk = reference0;
	
	if !("\q" _in_ _talk){
		_talk = REPLACE(_talk,". ",".\w[12] ")
		_talk = REPLACE(_talk,"? ","?\w[22] ")
		_talk = REPLACE(_talk,", ",",\w8 ")
		
	}
	
	_talk = REPLACE(_talk,"Empyria ","Empyria \![raise,OnHeardEmpyria,0]")
     
    if reference1 == "" && reference2 == ""
    { //send input box : no event (ref2) , no special flag (ref1)
        EVAL('"' + REPLACE(_talk,'"','" + CHR(0x22) + "') + '"')
    }
    else
    { //event result from ghost
        TOSTR(_talk);
    }
}
//"
//above comment is bc my code styler is freakin out 

//------------------------------------------------------------------------------
//OnTranslateInternalイベント
//------------------------------------------------------------------------------
//OnTranslateがYAYA→本体→YAYAと、一度本体を経由するのに対して、
//OnTranslateInternalはYAYA内部でのトランスレートです。
//そのため、reference変数などが変更されない状態でこの関数に渡されます。
//引数は、_argv[0]が、本体に渡す寸前のトークの内容です。
//動かないようにコメントアウトしています。
/*
OnTranslateInternal
{
	_text = _argv[0]
	_text = REPLACE(_text, "。", "にゅ。")
	_text
}
*/


//just a few quick things that'll set some variables
OnMetVanta{
	vantaknown = 1
}


OnMetRook{
	rookknown = 1
}

OnMetKorianne{
	korianneknown = 1
}

OnHeardEmpyria{
	empyria = 1
}