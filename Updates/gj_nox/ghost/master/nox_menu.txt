//******************************************************************************
// YAYAÅ@Ghost template
// menu processing dictionary
//******************************************************************************

//******************************************************************************
// Develop main menu
//
// How to use AYATEMPLATE.MenuItem
// first string = string to be displayed in choice
// second string = function name executed when selected
// 3 second character string = line feed amount / 50% increments / 100% if omitted (= \ n)
//******************************************************************************

//Constants
#globaldefine ANNOYEDR3 -250
#globaldefine ANNOYEDR2 -150
#globaldefine ANNOYEDR1 -50
#globaldefine POSITIVER1 50
#globaldefine POSITIVER2 150
#globaldefine POSITIVER3 250

#globaldefine QPERHOUR 5
#globaldefine MAXPETS 5
#globaldefine MAXWEIGHT 20

//------------------------------------------------------------------------------
//Main menu
//------------------------------------------------------------------------------
OpenMenu
{
	lastinteraction = 0
	if tendiesallowed == 1{
		OnEndTendrils
	}
	"\b1"
	--
	//N, P1, P2, P3
	if relationship > ANNOYEDR1{
		"%(noxCurious)Oh? Do you need something?"
		"%(noxNeutral)Hmm?"
		"%(noxNeutral)Need something?"
	}else{
	//A3, A2, A1
		"%(noxAnnoyed)What?"
	
	}
	
	if relationship < ANNOYEDR2{
		"%(noxNarAngry)(He glares at you.)"
	}
	
	//A1, N, P1, P2, P3
	if relationship > ANNOYEDR2 && RAND(2)==0{
		"%(noxCurious)What is it?"
	
	}
	
	--
	AYATEMPLATE.MenuItem("Say something","Menu_AITALK",200)
	--
	if ( STRLEN(AYATEMPLATE.LastTalk) > 0 ) {
		AYATEMPLATE.MenuItem("What was that?","Menu_LASTTALK")
	}
	--
	AYATEMPLATE.MenuItem("Hey\, I have a question","Menu_QUESTION")
	AYATEMPLATE.MenuItem("Hey\, I have a question","Menu_QUESTION")
	AYATEMPLATE.MenuItem("I have a question...","Menu_QUESTION")
	--
	AYATEMPLATE.MenuItem("I have something for you","Menu_GIFTS")
	AYATEMPLATE.MenuItem("I have something for you","Menu_GIFTS")
	AYATEMPLATE.MenuItem("I got you something","Menu_GIFTS")
	--
	AYATEMPLATE.MenuItem("Talk to another ghost","Menu_GHOSTCOMSTART")
	--
	AYATEMPLATE.MenuItem("Settings","Menu_SETTINGS",150)
	--
	AYATEMPLATE.MenuItem("Nevermind","Menu_CANCEL",150)
}

//------------------------------------------------------------------------------
//ÉÅÉjÉÖÅ[ëIëÉ^ÉCÉÄÉAÉEÉg
//------------------------------------------------------------------------------
OnChoiceTimeout
{
	"\b1"
	--
	//N, P1, P2, P3
	if relationship > ANNOYEDR1{
		"%(noxNeutral)It's okay, take your time."
		"%(noxNeutral)Let me know if you need me."
	}else{
	//A3, A2, A1
		"%(noxNarAnnoyed)(He loses interest.)"
	
	}
	
	//A1, N, P1, P2, P3
	if relationship > ANNOYEDR2 && RAND(2)==0{
		"%(noxCurious)Uuh- Alright?"
	
	}
	--
	"\e"

}




//---- Talk with the user
Menu_COMMUNICATEOPEN
{
	"%(noxNeutral)What is it?\![open,communicatebox]\e"
}

//---- talk to a friendghost
Menu_GHOSTCOMSTART
{
	StartCommunicate
}

//---- Nevermind
Menu_CANCEL
{
	"\b1"
	--
	//A1, N, P1, P2, P3
	if relationship > ANNOYEDR2{
		"%(noxNeutral)Oh, alright then."
		"%(noxNeutral)Oh was that all?"
	
	}
	
	//A2, A1, N, P1, P2, P3
	if relationship > ANNOYEDR3{
		"%(noxNeutral)Alright."

	}
	
	//A2, A1, N
	if relationship < POSITIVER1 && relationship > ANNOYEDR3{
		"%(noxNeutral)Mmm."
	}
	
	if relationship < ANNOYEDR2{
		"%(noxNarAnnoyed)(He continues glaring at you.)"
	}
	
	
	
	--
	"\e"
}






//******************************************************************************
//äeçÄñ⁄ÇÃèàóùì‡óe
//******************************************************************************

//---- Call the random talk
Menu_AITALK
{
	OnAiTalk
}

//---- last thing said, returns a string variable
Menu_LASTTALK
{
	AYATEMPLATE.LastTalk
}






//------------------------------------------------------------------------------
//Questions menu
//------------------------------------------------------------------------------

Menu_QUESTION{
	"\b2%(noxNarBase)(Ask about...)"
	--
	AYATEMPLATE.MenuItem("I need to say something","Menu_COMMUNICATEOPEN",200)
	--
	if questioncooldown > ((QPERHOUR-1) * cdmultiplier){
		AYATEMPLATE.MenuItem("Himself","Menu_QUESTION.cooldown",150)
		--
	//	AYATEMPLATE.MenuItem("His past","Menu_QUESTION.cooldown")
		--
		AYATEMPLATE.MenuItem("His home","Menu_QUESTION.cooldown")
		--
	}else{
		AYATEMPLATE.MenuItem("Himself","Menu_QUESTION_SELF",150)
		--
	//	AYATEMPLATE.MenuItem("His past","Menu_QUESTION_PAST")
		--
		AYATEMPLATE.MenuItem("His home","Menu_QUESTION_HOME")
		--
	}
	--
//	AYATEMPLATE.MenuItem("Magic","Menu_QUESTION_MAGIC")
	--
	AYATEMPLATE.MenuItem("Other dragons","Menu_QUESTION_DRAGONS")
	--
	AYATEMPLATE.MenuItem("Humans","Menu_QUESTION_HUMANS")
	--
	if questioncooldown > ((QPERHOUR-1) * cdmultiplier){
		AYATEMPLATE.MenuItem("Aria","Menu_QUESTION.cooldown")
		--
		AYATEMPLATE.MenuItem("Rowan","Menu_QUESTION.cooldown")
		--
		AYATEMPLATE.MenuItem("Wren","Menu_QUESTION.cooldown")
		--
		
	}else{
		AYATEMPLATE.MenuItem("Aria","Menu_QUESTION_ARIA")
		--
		AYATEMPLATE.MenuItem("Rowan","Menu_QUESTION_ROWAN")
		--
		AYATEMPLATE.MenuItem("Wren","Menu_QUESTION_WREN")
		--
		
	}
	--
	if whathappened == 1{
	
		if vantaknown
			AYATEMPLATE.MenuItem("Vanta","Menu_QUESTION_VANTA")
			
		--
		if rookknown
			AYATEMPLATE.MenuItem("Rook","Menu_QUESTION_ROOK")
			
		--
		if korianneknown
			AYATEMPLATE.MenuItem("%(kori)","Menu_QUESTION_KORIANNE")
	
	}
	--
	if empyria > 0{
		AYATEMPLATE.MenuItem("Empyria","Menu_QUESTION_EMPYRIA")
	}
	--
	AYATEMPLATE.MenuItem("Back to menu","OpenMenu",150)


}

Menu_QUESTION_SELF{
	questioncooldown++
	
	//P1,P2,P3
	if relationship > POSITIVER1{
		"%(noxNarBase)(You ask Nox why he eats all of your gifts.)\n\n%(noxCurious)Where else would I put them? My robes?"
	}
	
	//A1, N, P1
	if relationship > ANNOYEDR1 && relationship < POSITIVER2{
		relationship+= RAND(3) + 3
		"%(noxNeutral)My name is Nox. Clearly, as you might be able to tell, I am a dragon."
		"%(noxCurious)I'm curious as to why you ask that. \w8%(noxNeutral) Seeing as I'm trapped here, perhaps you simply want to get to know me better. Hmmm... \w[16] Well, I have a preference of strongly seasoned foods. Will that suffice?"
		"%(noxCurious) Depending on where you're from, you might have heard of me before. %(noxNeutral) Well, I am a dragon. I likely already have several legends surrounding me."
	
	}
	
	//A1, A2
	if relationship < ANNOYEDR1 && relationship > ANNOYEDR3{
		"%(noxNarAnnoyed)(You ask Nox if he has ever eaten someone.)\n\n\%(noxCurious)Wh- what do you mean by that?\n(noxNarAnnoyed)(He isn't denying it.)"
	
	}
	
	if relationship < ANNOYEDR3{
		Menu_QUESTION.negDenial
	}
	--
	"\e"
	

}

//future update will implement
Menu_QUESTION_PAST{
	questioncooldown++

}


Menu_QUESTION_HOME{
	questioncooldown++
	whathappened = 1
	
	//P1, P2, P3
	if relationship > POSITIVER1{
		"%(noxBase)Hmmm. You seem trustworthy enough. I live in a village called Nighthaven with my people and family. ItÅfs a quiet place, separated away from the three magical kingdoms.\n\nWell, maybe it isnÅft quiet. %(noxNarNeutral)(He quietly chuckled to himself.)"
		"\![raise,OnMetVanta]%(noxNeutral)I have an adopted son named Vanta who lives with me. IÅfve taught him nearly everything I know about magic. The rest however? Well he learned himself! HeÅfs such a smart boy.\n\n%(noxNarNeutral)(HeÅfs absolutely beaming.)"
		"\![raise,OnMetRook]%(noxNeutral)If thereÅfs a notable resident of my home, it would be Rook. HeÅfs such an odd fellow whoÅfs always laughing and joking around. Life would be absolutely dull without him."
		"\![raise,OnMetKorianne]%(noxBase)Nighthaven used to have a doctor named %(kori), but %(koriHe) decided %(koriHe) would be better suited to life on the road. I hope that %(koriHe) is doing well.\n\nAh! I should also mention %(koriHe) goes by two names, being Kori and Korianne. Each name has their own set of pronouns, and I will be using those with their respective name."
	
	
	//A1, N
	}elseif relationship > ANNOYEDR2{
		"%(noxBase)Some secrets are best kept to oneself. Where I live isnÅft somewhere IÅfd be willing to disclose. Not yet."
	
	}
	
	//P2, P3
	if relationship > POSITIVER2{
		"%(noxNeutral)Really, Nighthaven was a lucky find for me. If it werenÅft for Eddy, I never would have been able to find out there was a nearby island. And an uninhabited one at that. So it was there I started a village.\n\n%(noxCurious)Granted, I had to befriend some pirates who kept their treasure there first."
		"%(noxNeutral)A lot of the older buildings at Nighthaven I built myself. Of course, after the population started to grow, the people started building them on their own. A lot of the original architecture is still in effect however."
	}
	
	if relationship < ANNOYEDR2{
		Menu_QUESTION.negDenial
	}else{
		relationship+= RAND(3) + 3
	}
	--
	"\e"
}

//future update will have
Menu_QUESTION_MAGIC{


}


Menu_QUESTION_DRAGONS{

	//A1, N, P1, P2, P3
	if relationship > ANNOYEDR2{

		"%(noxNeutral)Including myself, there are six dragons in total. That leaves one dragon per element. Granted, I'm only on good terms with about one of them. \w[16]Well maybe two, but I haven't seen him in a century at least."
		"%(noxCurious)I wonder who the current Fire dragon is. I have not made contact with the latest one yet, so I can only wonder what they're like.\w[16]\n%(noxNeutral)If I have to be blunt, they're probably just as hotheaded as the last one."
		"%(noxNeutral)If I had to pick the most agreeable dragon, it would likely be Nymph the Plant Dragon. SheÅfs the kindest of the lot and the only one whoÅfs really willing to talk to me."
		"%(noxCurious)I wonder where Eddy wentÅc I havenÅft seen him in at least a century. Then again, he isnÅft exactly good at staying in one place.\w8.\w8.\w8\n\n%(noxNeutral)Ah, what were we talking about again?"
		"%(noxNarCurious)(You ask Nox about the Dragon of Light.)\n.\w8.\w8.\w8.\w8.\w8.\w8.\n%(noxAnnoyed)....No."
		//rip Shard
		"%(noxNeutral)Frankly, I havenÅft spoken much with the new Wind Dragon. I know her name is Ariel and she takes an unusual serpentine shape, but beyond that IÅfm not sure."
	
		//rip Shard pt2
		if relationship > POSITIVER3{
			"%(noxNarCurious)(You ask Nox about the Dragon of Light.)\n.\w8.\w8.\w8.\w8.\w8.\w8.\n%(noxAnnoyed)%(noxEyesClose).\w8.\w8.\w8.His name is Shard. HeÅfs just as ancient as myself, and a traitor. I still donÅft know why to this day why he tried to kill me.\n\n%(noxAnnoyed)....We used to be such good friendsÅc."
			"%(noxNeutral)A long time ago, dragons agreed on a sort of rule where they werenÅft supposed to have children. A dumb rule, I know.\w8\w8\n\nSadly though, most of the dragons at the time agreed to it. And since then, I know of at least one dragon who broke that rule. And if my theory is correctÅc\w8\w8 There may be another."
		}
	
	
	}elseif relationship < ANNOYEDR3{
		"%(noxAngry) You remind me of one of them. \w[16]%(noxAnnoyed) That is not a compliment."
	
	}else{
		//A2
		Menu_QUESTION.negDenial
	
	}
	--
	"\e"

}


Menu_QUESTION_HUMANS{
	"\b1"
	--
	if relationship < ANNOYEDR2{
		"%(noxAnnoyed)Phosphrinians are sometimes called Expanders because of how much land the country tries to grab all at once. Their actions make me upset, but at least theyÅfre nicer than you."
	}else{
		"%(noxNeutral)Sequoia is one of the three major magical kingdoms. ItÅfs more plant based than any of them, which cause a lot of the residents to become farmers. There isnÅft any other place in the world that has more fertile soil than them."
		"%(noxNeutral)Infernus is technically a country, but in name only. The reality of the situation is that the land is broken up by tribes. However, due to the threat Phosphrin poses, they all agree to pose as one entity to prevent any more land being stolen."
		"%(noxNeutral)I remember a time where Phosphrin was just a small country that no one paid much attention to. However, about a century ago, they started conquering the neighboring territories. The country grew fast, but its growth has stopped due to EmpyriaÅfs territory becoming hostile itself and Infernus refusing to give away to any more territory."
	}

}

Menu_QUESTION_ARIA : sequential {
	questioncooldown++
	"\b1"
	--
	if relationship > POSITIVER3{
		relationship+= RAND(3) + 3
		"%(noxNeutral) I need to keep her safe. Not only for my sake, but on the promise of a friend as well. I can't say who this friend is, young one, but I don't think he would be well pleased with how events are currently turning out. \x%(noxCurious) (He frowns)\n\nHm? Why would my friend not like current events? Simple. I got her thrown out of her village. Literally. Some guardian I am. I can't even protect one person."
		
	}elseif relationship > POSITIVER2{
		relationship+= RAND(3) + 3
		"Hmm. That isÅc not a name you should know, and yet you know it anyways."
		"%(noxNeutral) I'm not sure how you came to know I was following her. No one else was in Death's Cliffs as far as I was aware."
		"%(noxNeutral)%(noxEyesClosed) She's a powerful magic user, and a wind mage at that. \w[16]%(noxNeutral) Ever since the fall of Empyria, those are hard to come by."
	
	}elseif relationship > ANNOYEDR2{
		"%(noxCurious) Who?"
		"%(noxNarNeutral)(He seems to be confused by your question.)"
		"%(noxNeutral)Hmm. I'm afraid the name doesn't ring a bell."
		"%(noxNeutral)I don't know someone named Aria."
		"%(noxNarNeutral) (He remained silent.)"
	
	}elseif relationship > ANNOYEDR3{
		"%(noxAnnoyed) Why is this something you need to know?"
	
	}else{
		Menu_QUESTION.negDenial
		"%(noxAngry)If you value your life at all, you will stay away from her."
	}
	--
	"\e"

}


Menu_QUESTION_ROWAN{
	questioncooldown++
	"\b1"
	--
	if relationship > POSITIVER2{
		relationship+= RAND(3) + 3
		{
			"%(noxCurious)It is strange to me that you mention him. Are you from Sequoia perhaps? \x %(noxNeutral) No matter. He's a good man, but he seems to be walking misguided footsteps."
			"%(noxNeutral)It's true that he transitioned his magic from plant to fire. But is he more powerful for it? \w[16]%(noxAnnoyed)No. He is much weaker."
			"%(noxNeutral)I wish I knew his intentions. \w[16]It isn't traveling season for merchants, so there really wasn't any reason for him to be traveling through Death's Cliffs. He saved Aria though, to which I'm grateful."
			
		}
	
		if relationship > POSITIVER3{
			
			"%(noxNeutral)Rowan strikes me as odd. Most people would have given into disparity with the tragedies he has experienced. But instead he's traveling to a different country. UnlessÅc \w[16] Hmm. I don't like where that thought was going."
			"%(noxNeutral)In a way, he reminds me of myself. Whether that is a good or bad thing, well. I'll have to see."
			
		}
		
		
	
	}elseif relationship > ANNOYEDR2{
		"%(noxCurious) Who?"
		"%(noxNarNeutral)(He seems to be confused by your question.)"
		"%(noxNeutral)Hmm. I'm afraid the name doesn't ring a bell."
		"%(noxNeutral)I don't know someone named Rowan."
		"%(noxNarNeutral) (He remained silent.)"
	
	}else{
		Menu_QUESTION.negDenial
	
	}
	--
	"\e"

}


Menu_QUESTION_WREN{
	questioncooldown++
	
	"\b1"
	--
	if relationship > ANNOYEDR2{
		relationship+= RAND(3) + 3
	
		"%(noxCurious) Who?"
		"%(noxNarNeutral)(He seems to be confused by your question.)"
		"%(noxNeutral)Hmm. I'm afraid the name doesn't ring a bell."
		"%(noxNeutral)I don't know someone named Wren."
		"%(noxNarNeutral) (He remained silent.)"
	
	}else{
		Menu_QUESTION.negDenial
	}
	--
	"\e"

}


Menu_QUESTION_VANTA{
	"I spoke to Vanta earlier today and %(vantaMood)"
	"Oh Vanta? I checked in on him recently and %(vantaMood)"
	--
	"\e"


}

Menu_QUESTION_ROOK{
	"I spoke to Rook earlier today and %(rookMood)"
	"Oh Rook? I checked in on him recently and %(rookMood)"
	--
	"\e"

}

Menu_QUESTION_KORIANNE{
	"I spoke to %(kori) earlier today and %(korianneMood)"
	"Oh %(kori)? I checked in on %(koriHim) recently and %(korianneMood)"
	--
	"\e"

}



Menu_QUESTION_EMPYRIA{
	questioncooldown++
	"\b1"
	--
	if relationship > ANNOYEDR2{
		relationship+= RAND(3) + 3
		"%(noxNeutral)AhÅc such a shame what happened to it. I was not around when it all happened, but the kingdom was torn apart by a feral magic user. There were so many casualtiesÅc\w[16] the landscape is still recovering, and rumor has it the feral is still out there. I would advise against traveling through the ruins."
	}else{
		Menu_QUESTION.negDenial
	}
	--
	"\e"

}



Menu_QUESTION.cooldown{
	"\b1"
	--
	"%(noxCurious)Don't you think that is enough questions?"
	"%(noxNeutral)Hmm. Perhaps another time, young one."
	"%(noxNeutral)I would rather not say right now."
	"%(noxNeutral)A bit much, don't you think?"
	"%(noxNarNeutral)(He gently shakes his head.)"
	--
	"\e"

}

Menu_QUESTION.negDenial{
	"\b1"
	--
	"%(noxNarAnnoyed)(He ignored your question.)"
	"%(noxNarAngry)(He glares at you in response.)"
	"%(noxAngry)Why should I grace you with an answer?"
	"%(noxAngry)No."
	"%(noxNarAngry)(A deep throated growl emits from his mouth.)"
	"%(noxNarAngry)(You should probably apologize before asking questions.)"
	--
	"\e"

}










//------------------------------------------------------------------------------
//Gift Menu
//------------------------------------------------------------------------------

Menu_GIFTS{
	"%(noxCurious)\b1"
	--
	"What is it?"
	"What did you get?"
	"Oh...?"
	--
	"\_q\n\n\*"
	--
	//and now it is time for me to figure out how to manually space these instead of using AYATEMPLATE stuff, lol
	"\![*]\q[A book,Menu_GIFTS.BOOK]                    \![*]\q[Something shiny,Menu_GIFTS.SHINY]\n"
	--
	"\![*]\q[Something sweet,Menu_GIFTS.SWEET]     \![*]\q[Something spicy,Menu_GIFTS.SPICY]\n"
	--
	"\![*]\q[Something sour,Menu_GIFTS.SOUR]       \![*]\q[Bandages,Menu_GIFTS.BANDAGES]\n"
	--
	"\![*]\q[A mug,Menu_GIFTS.MUG]"
	--
	AYATEMPLATE.MenuItem("Nevermind","Menu_CANCEL",150)
	--
	"\e"

}




//buffs Vanta
Menu_GIFTS.BOOK{
	_id = RAND(ARRAYSIZE(bookgenreA))
	_gift = bookgenreA[_id]
	
	//deny by relationship
	if relationship < (ANNOYEDR3 + 50){
		"%(noxNarBase)(You tried to give Nox a book about %(_gift).)\n\n"
		--
		Menu_GIFTS.DENYBYRELATIONSHIP
	//deny by weight
	}elseif inventoryweight >= 20{
		"%(noxNarBase)(You tried to give Nox a book about %(_gift).)\n\n"
		--
		Menu_GIFTS.DENYBYWEIGHT
	//accept
	}else{
		vanta[3]++
		inventory,= 100 + _id
		relationship+= RAND(4) + 3
		inventoryweight+=1
		inventorylength++
		booksgiven++
		"%(noxNarBase)(You give Nox a book about %(_gift).)\n\n"
		--
		"%(noxCurious)Oh, a book? %(noxNeutral)Thank you, IÅfll put it in my library at home.\n%(noxNarNeutral)(He eats it, putting it into his inventory.)"
	
	}
	--
	"\e"
}

//buffs Rook
Menu_GIFTS.SHINY{
	_id = RAND(ARRAYSIZE(treasureA))
	_gift = treasureA[_id]
	
	//deny by relationship
	if relationship < (ANNOYEDR3 + 50){
		"%(noxNarBase)(You tried to give Nox %(_gift).)\n\n"
		--
		Menu_GIFTS.DENYBYRELATIONSHIP
	//deny by weight
	}elseif inventoryweight >= 20{
		"%(noxNarBase)(You tried to give Nox %(_gift).)\n\n"
		--
		Menu_GIFTS.DENYBYWEIGHT
	//accept
	}else{
		rook[3]++
		inventory,= 200 + _id
		relationship+= RAND(3) + 2
		inventoryweight+=1
		inventorylength++
		foodgiven++
		"%(noxNarBase)(You give Nox %(_gift).)\n\n"
		--
		"%(noxCurious)Oh whatÅfs this? How odd, IÅfll have to take a look at it when I get home.\n%(noxNarNeutral)(He eats it, putting it into his inventory.)"
	
	}
	--
	"\e"
}



//buffs Vanta
Menu_GIFTS.SWEET{
	_id = RAND(ARRAYSIZE(sweetfoodA))
	_gift = sweetfoodA[_id]
	
	//deny by relationship
	if relationship < (ANNOYEDR3 + 50){
		"%(noxNarBase)(You tried to give Nox some sweet food.)\n\n"
		--
		Menu_GIFTS.DENYBYRELATIONSHIP
	//deny by weight
	}elseif inventoryweight >= 20{
		"%(noxNarBase)(You tried to give Nox some sweet food.)\n\n"
		--
		Menu_GIFTS.DENYBYWEIGHT
	//accept
	}else{
		vanta[3]++
		inventory,= 300 + _id
		relationship+= RAND(7)
		inventoryweight+=1
		inventorylength++
		foodgiven++
		"%(noxNarBase)(You give Nox some sweet food.)\n\n"
		--
		"%(noxCurious)What is this? Some %_gift you say? Well, IÅfm sure IÅfll find a use for it.\n%(noxNarNeutral)(He eats it, but did he put it into his inventory?)"
	
	}
	--
	"\e"
	
}



//buffs Rook
Menu_GIFTS.SPICY{
	_id = RAND(ARRAYSIZE(spicyfoodA))
	_gift = spicyfoodA[_id]
	//deny by relationship
	if relationship < (ANNOYEDR3 + 50){
		"%(noxNarBase)(You tried to give Nox some spicy food.)\n\n"
		--
		Menu_GIFTS.DENYBYRELATIONSHIP
	//deny by weight
	}elseif inventoryweight >= 20{
		"%(noxNarBase)(You tried to give Nox some spicy food.)\n\n"
		--
		Menu_GIFTS.DENYBYWEIGHT
	//accept
	}else{
		rook[3]++
		inventory,= 400 + _id
		relationship+= RAND(7)
		inventoryweight+=1
		inventorylength++
		foodgiven++
		_gift = CAPITALIZEFIRST(_gift)
		"%(noxNarBase)(You give Nox some spicy food.)\n\n"
		--
		"%(noxCurious)What is this? %_gift you say? Well, IÅfm sure IÅfll find a use for it.\n%(noxNarNeutral)(He eats it, but did he put it into his inventory?)"
	
	}
	--
	"\e"
}



//buffs Kori/anne
Menu_GIFTS.SOUR{
	_id = RAND(ARRAYSIZE(sourfoodA))
	_gift = sourfoodA[_id]
	//deny by relationship
	if relationship < (ANNOYEDR3 + 50){
		"%(noxNarBase)(You tried to give Nox some sour food.)\n\n"
		--
		Menu_GIFTS.DENYBYRELATIONSHIP
	//deny by weight
	}elseif inventoryweight >= 20{
		"%(noxNarBase)(You tried to give Nox some sour food.)\n\n"
		--
		Menu_GIFTS.DENYBYWEIGHT
	//accept
	}else{
		korianne[3]++
		inventory,= 500 + _id
		relationship+= RAND(7)
		inventoryweight+=1
		inventorylength++
		foodgiven++
		"%(noxNarBase)(You give Nox some sour food.)\n\n"
		--
		"%(noxCurious)What is this? Some %_gift you say? Well, IÅfm sure IÅfll find a use for it.\n%(noxNarNeutral)(He eats it, but did he put it into his inventory?)"
	
	}
	--
	"\e"
}



//buffs Kori/anne
Menu_GIFTS.BANDAGES{
	_id = RAND(ARRAYSIZE(bandagesA))
	_gift = bandagesA[_id]
	//deny by relationship
	if relationship < (ANNOYEDR3 + 50){
		"%(noxNarBase)(You tried to give Nox %(_gift).)\n\n"
		--
		Menu_GIFTS.DENYBYRELATIONSHIP
	//deny by weight
	}elseif inventoryweight >= 20{
		"%(noxNarBase)(You tried to give Nox %(_gift).)\n\n"
		--
		Menu_GIFTS.DENYBYWEIGHT
	//accept
	}else{
		korianne[3]++
		inventory,= _id
		relationship+= RAND(4) + 1
		inventoryweight+=1
		inventorylength++
		bandagesgiven++
		"%(noxNarBase)(You give Nox %(_gift).)\n\n"
		--
		"%(noxCurious)WhatÅfs this? Medical supplies? What use would I have for those? %(noxNeutral)Well, I do have a friend who could make use of it.\n%(noxNarNeutral)(He eats it, putting it into his inventory.)"
	
	}
	--
	"\e"
}



//MUG.... EVENTUAL MUG KINGDOM
Menu_GIFTS.MUG{
	_id = RAND(ARRAYSIZE(mugdescA))
	_gift = mugdescA[_id]
	//deny by relationship
	if relationship < (ANNOYEDR3 + 50){
		"%(noxNarBase)(You tried to give Nox a %(_gift) mug.)\n\n"
		--
		Menu_GIFTS.DENYBYRELATIONSHIP
	//deny by weight
	}elseif inventoryweight >= 20{
		"%(noxNarBase)(You tried to give Nox a %(_gift) mug.)\n\n"
		--
		Menu_GIFTS.DENYBYWEIGHT
	//accept
	}else{
		inventory,= 900 + _id
		relationship+= RAND(2) + 1
		inventoryweight+=1
		inventorylength++
		mugsgiven++
		"%(noxNarBase)(You give Nox a %(_gift) mug.)\n\n"
		--
		"%(noxCurious)Oh whatÅfs this? How odd, IÅfll have to take a look at it when I get home.\n%(noxNarNeutral)(He eats it, putting it into his inventory.)"
	
	}
	--
	"\e"
}






Menu_GIFTS.DENYBYWEIGHT{
	"%(noxNeutral)Oh, another gift? IÅfm sorry.\w8\w8 IÅfm afraid I canÅft fit anything else in my inventory.\w8\n%(noxNarNeutral)(He gives it back.)"
	"%(noxNeutral)Oh, another gift? IÅfm sorry.\w8\w8 IÅfm afraid I canÅft fit anything else in me.\w8\n%(noxNarNeutral)(He gives it back.)"
	//""
	
}

Menu_GIFTS.DENYBYRELATIONSHIP{
	"%(noxNarAngry)(He grabs your gift, eats it, and spits it out destroyed.)"

}










//------------------------------------------------------------------------------
//Config menu
//------------------------------------------------------------------------------

Menu_SETTINGS{
	"%(noxBase)Here are the settings, what do you need to change?"
	--
	AYATEMPLATE.MenuItem("Change name from %username","Menu_UPDATENAME",200)
	--
	AYATEMPLATE.MenuItem("Change pronouns from %userthey/%userthem","Menu_UPDATEPRONOUNS",100)
	--
	AYATEMPLATE.MenuItem("Change talk rate","Menu_TALKINTERVAL",100)
	--
	if hourannounce >= 1{
		AYATEMPLATE.MenuItem("Turn off hourly announcements","Menu_HOURANNOUNCEMENTSOFF",100)
	}else{
		AYATEMPLATE.MenuItem("Turn on hourly announcements","Menu_HOURANNOUNCEMENTSON",100)
	}
	--
	if tendiesallowed >= 1{
		AYATEMPLATE.MenuItem("Turn off tendrils","Menu_TENDIESOFF",100)
	}else{
		AYATEMPLATE.MenuItem("Turn on tendrils","Menu_TENDIESON",100)
	}
	--
	AYATEMPLATE.MenuItem("Back","OpenMenu",150)


}

Menu_UPDATENAME{
	"What would you like me to call you?\![open,inputbox,OnMenu_NAMEUPDATED,0,]"
}

OnMenu_NAMEUPDATED{
	username = reference[0]
	"%(noxNeutral)Nice to meet you,\w8 %(username)."

}

Menu_UPDATEPRONOUNS{
	"\tPray tell,\w8 what pronouns do you prefer?"
	--
	AYATEMPLATE.MenuItem("They/Them/Theirs","Menu_THEY",200)
	--
	AYATEMPLATE.MenuItem("She/Her/Hers","Menu_SHE")
	--
	AYATEMPLATE.MenuItem("He/Him/His","Menu_HE")
	--
	AYATEMPLATE.MenuItem("Something else","Menu_NEO_A")


}


Menu_NEO_A{
	"%(noxCurious)Other pronouns? \w[16]%(noxNeutral)Alright then. \xWhat would go in place of they?\![open,inputbox,OnMenu_NEO_B,0,]"

}

OnMenu_NEO_B{
	userthey = reference0
	"What would go in place of them?\![open,inputbox,OnMenu_NEO_C,0,]"

}

OnMenu_NEO_C{
	userthem = reference0
	"What would go in place of their? \![open,inputbox,OnMenu_NEO_D,0,]"

}

OnMenu_NEO_D{
	usertheir = reference0
	"What would go in place of theirs? \![open,inputbox,OnMenu_NEO_End,0,]"

}

OnMenu_NEO_End{
	usertheirs = reference0
	"Okay, you'll use %userthey/%userthem/%usertheirs pronouns.\e"
}



Menu_HE{
	userthey		 = "he"
	userthem		 = "him"
	usertheir		 = "his"
	usertheirs		 = "his"
	OnMenu_PronounEnd

}

Menu_SHE{
	userthey		 = "she"
	userthem		 = "her"
	usertheir		 = "her"
	usertheirs		 = "hers"
	OnMenu_PronounEnd

}


Menu_THEY{
	userthey		 = "they"
	userthem		 = "them"
	usertheir 		 = "their"
	usertheirs		 = "theirs"
	OnMenu_PronounEnd

}

OnMenu_PronounEnd{
	"Okay, you'll use %userthey/%userthem/%usertheirs pronouns.\e"

}

//------------------------------------------------------------------------------
//Talk intervals
//------------------------------------------------------------------------------

//---- ÉgÅ[ÉNïpìxÇÃí≤êÆ
Menu_TALKINTERVAL
{
	"\b1%(noxNeutral)How often should I talk?"
	--
	AYATEMPLATE.MenuItem("Every minute","Menu_TALKMAX",200)
	--
	AYATEMPLATE.MenuItem("Every 3 minutes","Menu_TALKNORMAL")
	--
	AYATEMPLATE.MenuItem("Each 5 minutes","Menu_TALKPOOR")
	--
	AYATEMPLATE.MenuItem("Don't","Menu_TALKNONE")
	--
	AYATEMPLATE.MenuItem("Nevermind","OpenMenu",150)
}



//---- talk the most frequently
Menu_TALKMAX
{
	//---- Ç§ÇÈÇ≥Ç≠òbÇ∑(15ïbä‘äu)
	aitalkinterval = 15
	"This is a lot.\e"
}

Menu_TALKNORMAL
{
	//---- talk an average amount
	aitalkinterval = 180
	"Every three minutes, then.\e"
}

Menu_TALKPOOR
{
	//---- talk infrequently
	aitalkinterval = 300
	"Alright, sounds good.\e"
	"Every five minutes, then.\e"
}

Menu_TALKNONE
{
	//---- òbÇ≥Ç»Ç¢
	aitalkinterval = 0
	"Alright, I'll be quiet for now.\e"
}

//---- ÉwÉbÉhÉâÉCÉì
Menu_HEADLINECANCEL
{
	//---- ÉLÉÉÉìÉZÉãÇ≥ÇÍÇΩ
	"Alright, I'll put that away.\e"
}




Menu_HOURANNOUNCEMENTSOFF{
	hourannounce = 0
	"Alright.\e"

}

Menu_HOURANNOUNCEMENTSON{
	hourannounce = 1
	"Alright, I'll tell you on the hour.\e"

}

Menu_TENDIESOFF{
	OnEndTendrils
	--
	tendiesallowed = 0
	"Ah, ok."

}

//Turn it on, and have one spawn immediately for fun!
Menu_TENDIESON{
	tendiesallowed = 1
	"Alright then."
	//same energy as "so you have chosen death"
	//Dransnake: PEACE WAS NEVER AN OPTION
	--
	OnStartNoxTendrils

}
