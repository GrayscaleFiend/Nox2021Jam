//******************************************************************************
// YAYA@Ghost Template
// Boot and Close Dialogue
//******************************************************************************

//******************************************************************************
//OnFirstBoot
//******************************************************************************

OnFirstBoot
{
	//---- variable set up
	username         = "User"
	teachusername    = 0	// haven't learned the name yet
	relationship	 = 0
	questioncooldown = 0
	cdmultiplier	 = 1
	empyria          = 0
	hourannounce     = 1
	userthey		 = "they"
	userthem		 = "them"
	usertheirs		 = "theirs"
	usertheir		 = "their"
	inlockedevent    = 1
	
	mode 			 = 1    //Nox is awake to begin
	wakefulmovements = 0	//can make him more likely to wake
	timeslept 		 = 240   //note to self: replace with same logic you put in boot later
	
	
	pets			 = 0
	punches			 = 0
	wingtouch		 = 0	
	
	waspositive		 = 0    //betrayal notes
	wasnegative 	 = 0
	
	petsthishour	 = 0
	annoyance		 = 0
	tendiesallowed	 = 1		//release the shadow tenders
	lastinteraction	 = 0
	
	//Inventory will store a list of IDs of items
	inventory 		 = IARRAY
	inventorylength	 = 0
	inventoryweight  = 0		//MAXWEIGHT is declared in word
	
	
	//THE WHAT HAPPENED TODAY SYSTEM
	//Arrays are location - mood - special event - outstanding mood gain from gifts
	//The proper values will be assigned in OnWhatHappenedToday, so there's no need to handle things here, aside from starting mood.
	whathappened	 = 0 		//the user has to unlock this by asking about his home
	
	//VANTA
	vantaknown 		 = 0
	vanta 			 = IARRAY
	vanta 			 = (0,30,0,0)
	
	//ROOK
	rookknown 		 = 0
	rook  	 		 = IARRAY
	rook			 = (0,55,0,0)
	
	//KORI/ANNE
	korianneknown	 = 0
	korianne 		 = IARRAY
	korianne		 = (0,55,0,0,0)
	
	--
	OnWhatHappenedToday
	--

	//---- The real first time they've spawned
	//---- reference0 is the number of times Nox has been through FirstBoot
	
	//Since the scrolling thing exists, we have to make sure that Nox spawns in the right place
	"\0\![moveasync,--x=%(displaywidth-400),--y=%(displayheight-450),--time=0,--base=primaryscreen]"
	--
	"\t%(noxNeutral)Wh-\w8 what the?\w[20] Where am I?\w[12] How did I get here?\x %(noxCurious)Huh?\w6 A human?\w6 Why are you so big?\w[16] Unless I'm tiny? \nThat should be impossible,\w6 I'm huge.\w4.\w4. Or at least large to a human.\w8 Hmmm.\w4.\w4.\x %(noxNeutral)I don't think I have ever seen you before.\w[16] What would you like me to call you?"
	--
	"\![open,inputbox,OnNameTeachA,0,]"
		
}

OnNameTeachA{
	username = reference[0]
	"\t%(noxNeutral)Nice to meet you,\w8 %(username).\w[16] Pray tell,\w8 what pronouns do you prefer?"
	--
	AYATEMPLATE.MenuItem("They/Them/Theirs","FirstBoot_THEY",200)
	--
	AYATEMPLATE.MenuItem("She/Her/Hers","FirstBoot_SHE")
	--
	AYATEMPLATE.MenuItem("He/Him/His","FirstBoot_HE")
	--
	AYATEMPLATE.MenuItem("Something else","FirstBoot_NEO_A")


}
OnNameTeachB{
	username = reference[0]
	"\t%(noxNeutral)What pronouns would you like me to call you?"
	--
	AYATEMPLATE.MenuItem("They/Them/Theirs","FirstBoot_THEY",200)
	--
	AYATEMPLATE.MenuItem("She/Her/Hers","FirstBoot_SHE")
	--
	AYATEMPLATE.MenuItem("He/Him/His","FirstBoot_HE")
	--
	AYATEMPLATE.MenuItem("Something else","FirstBoot_NEO_A")


}

FirstBoot_NEO_A{
	"%(noxCurious)Other pronouns? \w[16]%(noxNeutral)Alright then. \xWhat would go in place of they?\![open,inputbox,OnFirstBoot_NEO_B,0,]"

}

OnFirstBoot_NEO_B{
	userthey = reference0
	"What would go in place of them?\![open,inputbox,OnFirstBoot_NEO_C,0,]"

}

OnFirstBoot_NEO_C{
	userthem = reference0
	"What would go in place of their? \![open,inputbox,OnFirstBoot_NEO_D,0,]"

}

OnFirstBoot_NEO_D{
	usertheir = reference0
	"What would go in place of theirs? \![open,inputbox,OnFirstBoot_NEO_END,0,]"

}

OnFirstBoot_NEO_END{
	usertheirs = reference0
	"To check if this is grammatically correct,\w8 can you tell me if this sentence is right? \w[22]\nI am sure there is no harm being around %userthem,\w8 as %userthey just brought me here onto %usertheir computer."
	--
	AYATEMPLATE.MenuItem("Yep!","FirstBoot_END",200)
	--
	AYATEMPLATE.MenuItem("No\, that's not it","FirstBoot_REINTRO")

}

FirstBoot_HE{
	userthey		 = "he"
	userthem		 = "him"
	usertheir		 = "his"
	usertheirs		 = "his"
	FirstBoot_RECAP

}

FirstBoot_SHE{
	userthey		 = "she"
	userthem		 = "her"
	usertheir		 = "her"
	usertheirs		 = "hers"
	FirstBoot_RECAP

}


FirstBoot_THEY{
	userthey		 = "they"
	userthem		 = "them"
	usertheir 		 = "their"
	usertheirs		 = "theirs"
	FirstBoot_RECAP

}

FirstBoot_RECAP{
	"\t%(noxNeutral)So to recap,\w8 you go by %(username) and you prefer %userthey/%userthem/%usertheirs pronouns.\w8\w8 Is this correct? \n(You can always change this later.)"
	--
	AYATEMPLATE.MenuItem("Yep!","FirstBoot_END",200)
	--
	AYATEMPLATE.MenuItem("No\, that's not it","FirstBoot_REINTRO")
	
}

FirstBoot_REINTRO{
	"\t%(noxCurious)No?\w[12] Alright. \xWhat is your name?\![open,inputbox,OnNameTeachB,0,]"

}

FirstBoot_END{
	inlockedevent = 0
	teachusername = 1
	"%(noxNeutral)Wonderful! Now. \n\w[16]%(noxCurious)Where am I?"

}




//******************************************************************************
//’Êí‚Ì‹N“® OnBoot
//******************************************************************************

OnBoot
{
	//---- surface set up
	"\1\s[-1]\0%(noxNeutral)"
	--
	//This will redirect him to other methods of opening if needed
	ONBOOTVARS
	--
	//---- boot checks
	_timeslot = GetTimeSlot
	
	case _timeslot{
	when "early"
	{
		//A1, N, P1, P2, P3
		if relationship > ANNOYEDR2{
			"%(noxNarNeutral)%(noxEyesClosed)%(noxYawn)(He let out a mighty yawn)\w[40]%(noxNeutral) It's a little early, don't you think?"
		
			//A1, A2, N, P1, P2
			if relationship < POSITIVER3{
				"%(noxAnnoyed)It's very early. What do you need?"
			}
		
		}
		
		//N, P1, P2, P3
		if relationship > ANNOYEDR1{
			"%(noxNarNeutral)%(noxEyesClosed)%(noxYawn)(He quietly yawned)\w[40]%(noxNeutral)It's quite early, do you need something?"
		}else{	//A1, A2, A3
			"%(noxAnnoyed)It is %(hour12). \w[16]I was asleep. \w[16]What do you want?"
		}
		
		//P1, P2, P3
		if relationship > POSITIVER1{
			"%(noxNeutral)%(noxEyesClosed)Mmmmnng. \w8%(noxNeutral)Oh hello... I was sleeping. Do you need something?"
		}
		
		//N, A1, A2, A3
		if relationship < POSITIVER1{
			"%(noxAnnoyed)I was sleeping. This better be good."
		}
		
		//A2, A3
		if relationship < ANNOYEDR2{
			"%(noxNarAngry)(He's glaring at you. You probably woke him up.)"
		}
		--
		"\e"
		
	}
	when "morning"
	{
		
		//N, P1, P2, P3
		if relationship > ANNOYEDR1 {
			"%(noxNeutral)Oh good morning! How are you today?"
			"%(noxNeutral)Oh hello there, did you need something?"
			"%(noxCurious)Here again? Oh alright."
		
		}else{	//A1, A2, A3
	
			//A1, A2
			if relationship < ANNOYEDR3 {
				"%(noxAnnoyed)Oh I'm back here again. What do you want?"
				"%(noxAnnoyed)What do you want?"
			}
		}
		
		
		//P2, P3
		if relationship > POSITIVER2 {
			"%(noxNeutral)Good morning, I hope you are doing well."
		
		}
		
		//N, A1, A2, A3
		if relationship < POSITIVER1 {
			
			"%(noxAnnoyed)I was busy with something, I hope you know. Make this quick please."
			
			//N, A1, A2
			if relationship > ANNOYEDR3 {
				"%(noxAnnoyed)Here again? Alright, make it quick."
			
			}
		}
		
		//A2, A3
		if relationship < ANNOYEDR2 {
			"%(noxAngry)What do you want?"
		
		}

	}
	when "afternoon"
	{
		//A1, N, P1, P2
		if relationship > ANNOYEDR2 && relationship < POSITIVER3 {
		
			"%(noxNeutral)Good afternoon, I hope you are doing well."
		}
		
		//N, P1, P2, P3
		if relationship > ANNOYEDR1{
			"%(noxNeutral)Oh hello there, did you need something?"
			"%(noxCurious)Here again? Oh alright."
		
		}else{	//A1, A2, A3
	
			//A1, A2
			if relationship < ANNOYEDR3{
				"%(noxAnnoyed)Oh I'm back here again. What do you want?"
				"%(noxAnnoyed)What do you want?"
			
			}
		}
		
		
		//P2, P3
		if relationship > POSITIVER2{
			"%(noxNeutral)Oh good afternoon! How are you today?"
		
		}
		
		//N, A1, A2, A3
		if relationship < POSITIVER1{
			"%(noxAnnoyed)I was busy with something, I hope you know. Make this quick please."
			
			//N, A1, A2
			if relationship > ANNOYEDR3{
				"%(noxAnnoyed)Here again? Alright, make it quick."
			
			}
		}
		
		//A2, A3
		if relationship < ANNOYEDR2{
			"%(noxAngry)What do you want?"
		
		}
	}
	when "night"
	{
        //N, P1, P2, P3
        if relationship > ANNOYEDR1{
			"%(noxNeutral)Good evening, I hope you are doing well."
			"%(noxNeutral)Oh hello there, did you need something?"
			"%(noxCurious)Here again? Oh alright."
        
        }else{    //A1, A2, A3
    
            //A1, A2
            if relationship < ANNOYEDR3{
				"%(noxAnnoyed)Oh I'm back here again. What do you want?"
				"%(noxAngry)What do you want?"
            }
        }

        //P2, P3
        if relationship > POSITIVER2{
			"%(noxNeutral)Oh good evening! How are you today?"
        
        }
        
        //N, A1, A2, A3
        if relationship < POSITIVER1{
			"%(noxAnnoyed)I was busy with something, I hope you know. Make this quick please."
            //N, A1, A2
            if relationship > ANNOYEDR3{
				"%(noxAnnoyed)Here again? Alright, make it quick."
            
            }
        }
        
        //A2, A3
        if relationship < ANNOYEDR2{
			"%(noxAngry)What do you want?"
        
        }
	}
	others
	{
		//other times
		"\0THIS IS A BUG\e"
	}
	}//case‚ÌI‚í‚è
}


OnReboot{

	_timeslot = GetTimeSlot
	case _timeslot{
	
		when "early"{
			//no reboot dialogues, so we leave the \e s for the individual sections so normal Boot dialogues will play
			
		}
		when "morning"{
			//N, P1, P2, P3
			if relationship > ANNOYEDR1{
				"%(noxCurious)Here again? Oh alright."
				"%(noxNeutral)Oh, did you need something else?"
			}else{
			
				//A1, A2
				if relationship > ANNOYEDR3{
					"%(noxAnnoyed)Oh I'm back here again. What do you want?"
				}
				
				//A2, A3
				if relationship < ANNOYEDR2{
					"%(noxAngry)Why did you bring me back?"
				}
			
			}
			
			//N, A1, A2
			if relationship < POSITIVER1 && relationship > ANNOYEDR3{
			
				"%(noxAnnoyed)Here again? Alright, make it quick."
			
			}
			--
			"\e"
		}
		when "afternoon"{
			//N, P1, P2, P3
			if relationship > ANNOYEDR1{
				"%(noxCurious)Here again? Oh alright."
				"%(noxNeutral)Oh, did you need something else?"
			}else{
			
				//A1, A2
				if relationship > ANNOYEDR3{
					"%(noxAnnoyed)Oh I'm back here again. What do you want?"
				}
				
				//A2, A3
				if relationship < ANNOYEDR2{
					"%(noxAngry)Why did you bring me back?"
				}
			
			}
			
			//N, A1, A2
			if relationship < POSITIVER1 && relationship > ANNOYEDR3{
			
				"%(noxAnnoyed)Here again? Alright, make it quick."
			
			}
			--
			"\e"
		}
		when "night"{
			//N, P1, P2, P3
			if relationship > ANNOYEDR1{
				"%(noxCurious)Here again? Oh alright."
				"%(noxNeutral)Oh, did you need something else?"
			}else{
			
				//A1, A2
				if relationship > ANNOYEDR3{
					"%(noxAnnoyed)Oh I'm back here again. What do you want?"
				}
				
				//A2, A3
				if relationship < ANNOYEDR2{
					"%(noxAngry)Why did you bring me back?"
				}
			
			}
			
			//N, A1, A2
			if relationship < POSITIVER1 && relationship > ANNOYEDR3{
			
				"%(noxAnnoyed)Here again? Alright, make it quick."
			
			}
			--
			"\e"
		}
	
	}

}

//------------------------------------------------------------------------------
//’©/’‹/–é‚È‚Ç‚ð‚¨‚¨‚´‚Á‚Ï‚É’²‚×‚éŠÖ”
//------------------------------------------------------------------------------
GetTimeSlot
{
	case hour{
	when 0,1,2,3,4,5 {'early'}
	when 6,7,8,9,10,11 {'morning'}
	when 12,13,14,15,16,17 {'afternoon'}
	when 18,19,20,21,22,23 {'night'}
	}//case
}


//******************************************************************************
//I—¹ OnClose
//******************************************************************************

OnForceClose{
	forceclose = 1
	ONCLOSEVARS
	"%(noxAngry)I've had about enough of this."
	"%(noxAnnoyed)If you are just going to pick on me, then I'm leaving."
	--
	"\w[30]\-"

}

OnForceCloseReboot{
	_timeslot = GetTimeSlot
	case _timeslot{
		when "early"
			"%(noxNarAngry)(He's glaring at you. He probably wanted to sleep.)"
		when "morning", "afternoon", "night"
			"%(noxAngry)Why did you bring me back?"
		
	}
	--
	" \e"
	

}


OnClose
{
	"%(noxNeutral)\1\s[-1]\0"
	--
	ONCLOSEVARS
	--
	//variables and timeslot
	//\ e is handled at the end
	
	_timeslot = GetTimeSlot
	if _timeslot == "early"
	{
		if mode{
			//A1, N, P1, P2, P3
			if relationship > ANNOYEDR2{
				"%(noxNarNeutral)%(noxEyesClose)%(noxYawn)\w6(He gently yawned) \w[50]%(noxNarNeutral)Is that all? \w6I'm going to sleep then."
			
				//A1, N, P1, P2
				if relationship < POSITIVER3{
					"%(noxNeutral)All done? Good, I want to go to sleep."
				}
			
			}
			
			//N, P1, P2, P3
			if relationship > ANNOYEDR1{
				"%(noxCurious)Staying up late? Better get some sleep soon."
			
			}
			
			//N, A1, A2, A3
			if relationship > ANNOYEDR1{
				"%(noxNarNeutral)%(noxEyesClose)(He seems relieved.)"

			}
			
			//A2, A3
			if relationship < ANNOYEDR2{
				"%(noxAnnoyed)Finally sending me home, hmm? It was about time."
			
			}
		}else{
		
			"%(noxNarSleep)(He should be able to sleep better at home.)"
		
		}
	}
	elseif _timeslot == "morning"
	{
	
		if mode{
			//A1, N, P1, P2, P3
			if relationship > ANNOYEDR2{
				"%(noxNeutral)Take care out there."
			
				//N, P1, P2, P3
				if relationship > ANNOYEDR1{
					"%(noxNeutral)Ah, time to go. Take care, %(username)."
					"%(noxNeutral)Ah, time to go. Take care, young one."
				}
			
			}
			
			//N, P1, P2, P3
			if relationship > ANNOYEDR1{
				"%(noxNeutral)Going so soon? That's alright. It is still early in the day, so there is plenty of time to go out and do things."
			
			}
			
			
			//A1, A2, A3
			if relationship < ANNOYEDR1{
				"%(noxNarAnnoyed)(He leaves without a word.)"
				
				//A1, A2
				if relationship > ANNOYEDR3{
				
					"%(noxNeutral)Oh good, I can go home. I have too much work to do."
				}
			
			}
		}else{
		
			"%(noxNarSleep)(He should be able to sleep better at home.)"
		
		}
	}
	elseif _timeslot == "afternoon"
	{
		if mode{
			//A1, N, P1, P2, P3
			if relationship > ANNOYEDR2{
				"%(noxNeutral)Take care out there."
			
				//N, P1, P2, P3
				if relationship > ANNOYEDR1{
					"%(noxNeutral)Ah, time to go. Take care, %(username)."
					"%(noxNeutral)Ah, time to go. Take care, young one."
				}
			
			}
			
			//N, P1, P2, P3
			if relationship > ANNOYEDR1{
				"%(noxNeutral)Going so soon? That's alright. I feel like the sun is still up high, so why not go take a walk outside?"
			
			}
			
			
			//A1, A2, A3
			if relationship < ANNOYEDR1{
				"%(noxNarAnnoyed)(He leaves without a word.)"
				
				//A1, A2
				if relationship > ANNOYEDR3{
				
					"%(noxNeutral)Oh good, I can go home. I have too much work to do."
				}
			
			}
		
		}else{
		
			"%(noxNarSleep)(He should be able to sleep better at home.)"
			"Zzzzzzz.... hmngh..."
		
		}
	}
	else
	{
		if mode{
			//---- night
			//A1, N, P1, P2, P3
			if relationship > ANNOYEDR2{
				"%(noxNeutral)Take care out there."
			
				//N, P1, P2, P3
				if relationship > ANNOYEDR1{
					"%(noxNeutral)Ah, time to go. Take care, %(username)."
					"%(noxNeutral)Ah, time to go. Take care, young one."
				}
			
			}
			
			//N, P1, P2, P3
			if relationship > ANNOYEDR1{
				"%(noxNeutral)Going so soon? That's alright. The sun is likely setting soon, so go get some well deserved rest."
			
			}
			
			
			//A1, A2, A3
			if relationship < ANNOYEDR1{
				"%(noxNarAnnoyed)(He leaves without a word.)"
				
				//A1, A2
				if relationship > ANNOYEDR3{
				
					"%(noxNeutral)Oh good, I can go home. I have too much work to do."
				}
			
			}
		}else{
		
			"%(noxNarSleep)(Time to let the tired drago sleep.)"
		
		}
	}
	--
	"\w9\w9\-\e"
}


//******************************************************************************
//Return OnWindowStateRestore from minimized
//******************************************************************************

OnWindowStateRestore
{
	if mode
		"%(noxCurious)Excuse me, but what was that?\e"
}














ONBOOTVARS{

	//Due to previous Issues, we have to reset position at the start of every session (It's for the best....)
	"\0\![moveasync,--x=%(displaywidth-400),--y=%(displayheight-450),--time=0,--base=primaryscreen]"
	--
	
	lastinteraction = 0
	if today == day{
		
		//Same day is treated differently, and Nox's annoyed ragequits are Special
		if forceclose == 1 && closehour == hour{
			annoyance = RAND(40) + 40 	//he'll try to do it again today!
			OnForceCloseReboot
			forceclose = 0
		}else{
			forceclose = 0
			OnReboot
		}
		
	
	
	}else{
		forceclose = 0
		OnWhatHappenedToday
		if relationship < POSITIVER1{
			annoyance = RAND(20)
	
		}
		inventoryweight = 0
		inventory = IARRAY
		
	
	}
	today = day
	
	mode = 1 //on boot, make him awake (may be modified later so ELEPHANT to find this if forgotten)
	

}

ONCLOSEVARS{
	//we don't use these relativity checks except on boot, so this should be fine
	closehour = hour
	//I think there was some reason this might end up problematic, but I can't remember it now.... hm.
	//	OKAY YEAH I REMEMBER NOW, UNCOMMENTING THIS WILL LOWKEY BREAK WHAT HAPPENED TODAY
	//  THE GANG WON'T GET MOOD BUFFS IF YOU LEAVE NOX OVER PAST MIDNIGHT THAT WAY RIP
	// ONREBOOT CAN STILL HAPPEN JUST NOT QUITE SO OFTEN, THIS SHOULD ACTUALLY ADD MORE DIVERSITY TO DIALOGUES THOUGH
	//TO GET ONREBOOT YOU HAVE TO OPEN AND CLOSE HIM SAME DAY
	//today = day
	
	//when Nox goes home he can spit up everything he stored in his stomach
	inventory 		 = IARRAY
	inventorylength	 = 0
	inventoryweight  = 0
	--
	OnEndTendrils

}

//WhatHappenedToday is located in nox_word.txt
